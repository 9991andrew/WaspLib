(*
TPointArray
===========
TPointArray related methods that extend SRL's TPoint functionality.
*)

{$DEFINE WL_TPOINTARRAY_INCLUDED}
{$IFNDEF WL_UTILS}
  {$I WaspLib/utils.simba}
{$ENDIF}

(*
TPointArray.ToString
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TPointArray.ToString(): String;

Prints the TPointArray in a way that can be copy pasted into and used by Simba.

Example
-------

  tpa := [[1,1], [2,2], [3,3]];
  WriteLn tpa.ToString();
*)
function TPointArray.ToString(endLine: Boolean = True): String;
var
  i: Int32;
begin
  Result := '[';
  for i := 0 to High(Self) do
  begin
    Result += '[' + ToStr(Self[i].X) + ', ' + ToStr(Self[i].Y) + ']';
    if i < High(Self) then
      Result += ', ';
  end;

  Result += ']';
  if endLine then
    Result += ';';
end;

(*
T2DPointArray.Connect
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function T2DPointArray.Connect(): T2DPointArray;

Connects each TPointArray in a T2DPointArray. Useful for debugging visually.

Example
-------

  Debug(atpa.Connect());
*)
function T2DPointArray.Connect(): T2DPointArray;
var
  i: Int32;
begin
  for i := 0 to High(Self) do
    Result += Self[i].Connect();
end;
