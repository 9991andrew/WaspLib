{$DEFINE WL_PROGRESS_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
var PadLength
~~~~~~~~~~~~~
Internal variable used to set control the PadLength in the progress report. This is usually set by `TBaseScript.BuildTextReport()`.

var PTimeRunning
~~~~~~~~~~~~~~~~
Internal StopWatch pointer to the TBaseScript.TimeRunning. This is required so antiban tasks/breaks/sleeps can modify TWaspProgressReport.

var ProgressArray
~~~~~~~~~~~~~~~~~
TODO: ADD INFO. Forgot what this was for exactly but something to do with antiban tasks same as above.
*)
var
  PadLength: Int32;
  PTimeRunning: ^TStopwatch;
  ProgressArray: TStringArray;

(*
type TWaspProgressReport
~~~~~~~~~~~~~~~~~~~~~~~~

Type responsible for handling the HUDProgressReport, also commonly called as "Paint" or InGame/OnScreen Progress Report.
*)
type
  TWaspProgressReport = record
    BGImg: TMufasaBitmap;

    TextColor: Int32;
    PreviousStrArray: TStringArray;

    Timer: TTimer;
    UsernameBox: TBox;

    Terminated: Boolean;
    IsSetup: Boolean;
  end;

(*
ProgressReport.GetNextCycleColor
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TWaspProgressReport.GetNextCycleColor(Color, Step: Int32): Int32;

Not currently used for anything, just a fun experiment. Can be used to give an RGB cycle effect.
You can see it in effect by uncommenting `Self.TextColor := Self.GetNextCycleColor(Self.TextColor, 10);` in **TWaspProgressReport.Update()**.
*)
function TWaspProgressReport.GetNextCycleColor(Color, Step: Int32): Int32;
var
  R, G, B: Int32;
begin
  ColorToRGB(Color, R, G, B);

  if (R = 255) and (G = 0) and (B < 255) then
    B += Step
  else if (R > 0)   and (G = 0)   and (B = 255) then
    R -= Step
  else if (R = 0)   and (G < 255) and (B = 255) then
    G += Step
  else if (R = 0)   and (G = 255) and (B > 0) then
    B -= Step
  else if (R < 255) and (G = 255) and (B = 0) then
    R += Step
  else if (R = 255) and (G > 0)   and (B = 0) then
    G -= Step;

  if R < 0    then R := 0;
  if R > 255 then R := 255;

  if G < 0    then G := 0;
  if G > 255 then G := 255;

  if B < 0    then B := 0;
  if B > 255 then B := 255;

  writeLn('R: ', R, ' G: ', G, ' B: ', B);
  Result := RGBtoColor(R, G, B);
end;

(*
ProgressReport.Terminate
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.Terminate();

Internal method called automatically on termination. This will do things such as free used assetsa and clear the progress report from the game screen.
*)
procedure TWaspProgressReport.Terminate();
begin
  if Self.Terminated then //avoids multiple terminations in case the script is chained.
    Exit;

  Self.BGImg.Clear();
  Self.BGImg.Free();

  RSClient.Image.Clear();
  RSClient.Image.Free(); //not sure if it needs to be freed?

  Self.Terminated := True; //avoids multiple terminations in case the script is chained.
end;

(*
ProgressReport.Setup
~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.Setup();

Internal method called when we need to setup TWaspProgressReport.
*)
procedure TWaspProgressReport.Setup();
begin
  Self.BGImg := TMufasaBitmap.Create();
  Self.BGImg.Clear();
  Self.BGImg.LoadFromFile({$MACRO CURRENT_DIRECTORY} + 'WaspProgress.png');
  Self.BGImg.setFontAntialiasing(True);

  RSClient.Image := RSClient.Image;
  RSClient.Image.Clear();
  RSClient.Image.setFontAntialiasing(True);
  RSClient.Image.setFontSize(13);

  {$IFDEF WINDOWS}
  Self.BGImg.setFontName('Courier New');      //It's hard to guess which fonts a Linux distro has available beforehand so I only set the font for windows.
  RSClient.Image.setFontName('Courier New');
  {$ENDIF}

  Self.TextColor := $006CFF;

  Self.UsernameBox := Chat.GetDisplayNameBox();

  AddOnTerminate(@Self.Terminate);
end;

(*
ProgressReport.DrawBackground
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.DrawBackground(StrArray: TStringArray; FontColor: Int32);

Method that handles drawing the background of our TWaspProgressReport.
Usually called internally by TWaspProgressReport.Update().
*)
procedure TWaspProgressReport.DrawBackground(StrArray: TStringArray; FontColor: Int32);
var
  P: TPoint;
begin
  if not Self.IsSetup then
  begin
    P := [10, 15];

    Self.BGImg.setFontSize(18);
    Self.BGImg.DrawTextShadow(StrArray[0], P, FontColor, False);

    Self.BGImg.setFontSize(14);
    P.Y += 105;
    FontColor -= $90A301 - $C8D100;
    Self.BGImg.DrawText(StrArray[4], P, FontColor);

    Self.IsSetup := True;
  end;

  RSClient.Image.DrawBitmap(Self.BGImg, [Chat.Bounds.X1, Chat.Bounds.Y1 - 12]);
end;

(*
ProgressReport.DrawProgress
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.DrawProgress(StrArray: TStringArray; FontColor: Int32);

Method that handles drawing the text of our TWaspProgressReport.
Usually called internally by TWaspProgressReport.Update().
*)
procedure TWaspProgressReport.DrawProgress(StrArray: TStringArray; FontColor: Int32);
var
  P: TPoint;
  i: Int32;
begin
  P := [Chat.Bounds.X1 + 10, Chat.Bounds.Y1 + 50];

  FontColor -= $C8D100;

  for i := 1 to 3 do
  begin
    if i > High(StrArray) then
      Break;

    RSClient.Image.DrawTextShadow(StrArray[i], P, FontColor, False);
    P.Y += 20;
  end;

  Self.PreviousStrArray := StrArray;
end;

(*
ProgressReport.HideProgress
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.HideProgress();

Used to hide TWaspProgressReport.
Usually called internally by TWaspProgressReport.Update().
*)
procedure TWaspProgressReport.HideProgress();
begin
  Self.IsSetup := False;

  RSClient.Image.DrawBoxFilled(Self.UsernameBox, $819DAC); //Hide username by painting a box with similar color to the chatbox background.
end;

(*
ProgressReport.Update
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.Update(StrArray: TStringArray);

Main method that should be called to used to start and update TWaspProgressReport.

To see TWaspProgressReport.GetNextCycleColor() in action, which was just a fun experiment,
uncomment `Self.TextColor := Self.GetNextCycleColor(Self.TextColor, 10);`
*)
procedure TWaspProgressReport.Update(StrArray: TStringArray);
begin
  RSClient.Image.Clear();

  if StrArray = [] then
    Exit;

  //Self.TextColor := Self.GetNextCycleColor(Self.TextColor, 10);
  Self.DrawBackground(StrArray, Self.TextColor);
  Self.DrawProgress(StrArray, Self.TextColor);
end;

(*
ProgressReport.Setup
~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TWaspProgressReport.Setup(); override;

Overriden method to enable TWaspProgressReport.Timer.
This required to be overriden or we couldn't call already declared methods in ProgressReport.CheckButton().
*)
procedure TWaspProgressReport.Setup(); override;
begin
  inherited;

  AddOnPause(@Self.HideProgress);
end;

(*
var ProgressReport
~~~~~~~~~~~~~~~~~~

Global TWaspProgressReport variable.
*)
var
  ProgressReport: TWaspProgressReport;
