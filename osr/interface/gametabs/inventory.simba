{$DEFINE WL_INVENTORY_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
InventoryExtra
==============
Methods to interact with the inventory.
This file expands on SRL's inventory file.
*)

const
  //Vertical 2 by 2 columns pattern. Normal and "reversed"
  DROP_PATTERN_TWO_ROW: TIntegerArray = [0,4,1,5,2,6,3,7,8,12,9,13,10,14,11,15,16,20,17,21,18,22,19,23,20,24,25,26,27,28];
  DROP_PATTERN_TWO_ROW_R: TIntegerArray = [0,4,1,5,2,6,3,7,11,15,10,14,9,13,8,12,16,20,17,21,18,22,19,23,20,24,28,27,26,25];

(*
TRSItem.Reorder
~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.Reorder(LowToHi: Boolean = True): TRSItem;

Used to retrieve reordered items.
Items that have several "doses" or "portions" like a Saradomin brew (1..4) might need to be reordered sometimes.

When we want to consume them, we usually want to prioritize lower dosage items to get inventory space.
However when we are withdrawing it from the bank, we want to prioritize the highest dosage items to maximize trips length.

This function reorders depending on what you set in **LowToHi**. By default it reorders items from low to high.
*)
function TRSItem.Reorder(LowToHi: Boolean = True): TRSItem;
var
  Str, first, last: String;
  i, j: Int32;
begin
  Str := ToStr(Self);

  if not Str.Contains('..') then
    Exit(Self);

  i := StrToInt(Str.Before('..')[High(Str.Before('..'))]);
  j := StrToInt(Str.After('..')[Low(Str.Before('..'))]);

  if (LowToHi and (i > j)) or (not LowToHi and (i < j)) then
  begin
    first := ToStr(j);
    last  := ToStr(i);
  end
  else
  begin
    first := ToStr(i);
    last  := ToStr(j);
  end;

  Result := Str.Replace('(' + ToStr(i) + '..' + ToStr(j) + ')', '(' + first + '..' + last + ')');
end;

(*
TRSItem.GetPortions
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetPortions(): Int32;

Get the amount of portions in the multi dose item (an item with "(x..z)").
*)
function TRSItem.GetPortions(): Int32;
var
  Str: String;
  i, j: Int32;
begin
  Str := ToStr(Self);

  if not Str.Contains('..') then
    Exit(1);

  i := StrToInt(Str.Before('..')[High(Str.Before('..'))]);
  j := StrToInt(Str.After('..')[Low(Str.Before('..'))]);

  if i > j then
    Result := i
  else
    Result := j
end;

(*
TRSItem.GetPortion
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetPortion(): Int32;

Get the portion number of one specific Item.

Example
-------
  Item := 'Saradomin brew(4)';

  WriteLn Item.GetPortion; //This will print 4.
*)
function TRSItem.GetPortion(): Int32;
var
  Str: String;
begin
  Str := ToStr(Self);

  if not Str.Contains('(') then
    Exit(1);

  Result := StrToInt(Str.Before(')')[High(Str.Before(')'))]);
end;

(*
Item.GetArray
~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetArray(): TRSItemArray;

Used to retrieve an item array of our multi dose/portion item.

Example
-------
  var
    Item: TRSItem;
  begin
    Item := 'Saradomin brew(1..4)';

    WriteLn Item.GetArray();
    //This will print: ['Saradomin brew(1)', 'Saradomin brew(2)', 'Saradomin brew(3)', 'Saradomin brew(4)']
  end;
*)
function TRSItem.GetArray(): TRSItemArray;
var
  Str: String;
  i, first, last: Int32;
begin
  Str := ToStr(Self);

  if not Str.Contains('..') then
    Exit([Self]);

  first := StrToInt(Str.Before('..')[High(Str.Before('..'))]);
  last :=  StrToInt(Str.After('..')[Low(Str.Before('..'))]);
  if first < last then
  begin
    for i := first to last do
      Result += Str.Replace(ToStr(first) + '..' + ToStr(last), ToStr(i));
  end
  else if first > last then
  begin
    for i := first downto last do
      Result += Str.Replace(ToStr(first) + '..' + ToStr(last), ToStr(i));
  end;
end;

(*
Item.GetSingle
~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetSingle(Lo: Boolean = True): TRSItem;

Used to retrieve the lowest or highest dose of our multi dose/portion item.

Example
-------
  var
    Item: TRSItem;
  begin
    Item := 'Saradomin brew(1..4)';

    WriteLn Item.GetSingle(True); //This will print: 'Saradomin brew(1)'
  end;
*)
function TRSItem.GetSingle(Lo: Boolean = True): TRSItem;
var
  Str: String;
  i, j: Int32;
begin
  Str := ToStr(Self);

  if not Str.Contains('..') then
    Exit(Self);

  i := StrToInt(Str.Before('..')[High(Str.Before('..'))]);
  j :=  StrToInt(Str.After('..')[Low(Str.Before('..'))]);

  if (Lo and (i > j)) or (not Lo and (i < j)) then
    Result := Str.Replace(ToStr(i) + '..' + ToStr(j), ToStr(j))
  else
    Result := Str.Replace(ToStr(i) + '..' + ToStr(j), ToStr(i));
end;



function TRSInventory.FindItems(Items: TRSItemArray; out Slots: TIntegerArray): Boolean; override;
var
  Item, SingleItem: TRSItem;
  i: Int32;
  TBA, tmp: TBoxArray;
  UsedSlots, TempSlots: TIntegerArray;
begin
  if not Self.Open() then
    Exit;

  TBA := Self.GetSlotBoxes();
  for i:=0 to High(TBA) do
    if Self.IsSlotUsed(TBA[i]) then
    begin
      tmp += TBA[i];
      UsedSlots += i;
    end;

  for Item in Items do
    for SingleItem in Item.GetArray() do
      TempSlots += ItemFinder.Find(SingleItem, tmp);

  for i in TempSlots do
    Slots += UsedSlots[i];

  Slots.ClearDuplicates();

  Result := Length(Slots) > 0;
end;


function TRSInventory.FindItem(Item: TRSItem; out Slot: Int32): Boolean; override;
var
  TempItem: TRSItem;
begin
  for TempItem in Item.GetArray() do
  begin
    Result := inherited(TempItem, Slot);

    if Result then
      Exit(True);
  end;
end;




function TRSInventory.ItemIsStackable(Item: TRSItem): Boolean;
begin
  Result := Self.CountItemStack(Item) > 0;
end;


function TRSInventory.FindAllItems(Items: TRSItemArray; out Slots: TIntegerArray): Boolean;
var
  Item: TRSItem;
begin
  for Item in Items do
  begin
    if not Self.FindItem(Item, Slots) then
      Exit(False);
    Slots := [];
  end;
  Result := True;
end;

function TRSInventory.FindAllItems(Items: TRSItemArray): Boolean; overload;
var
  Slots: TIntegerArray;
begin
  Result := Self.FindAllItems(Items, Slots);
end;



function TRSInventory.SetSelectedSlot(Slot: Int32): Boolean; override;
var
  Selected: Int32;
begin
  Selected := Self.GetSelectedSlot;
  if (Selected = Slot) then
    Exit(True);

  if (Selected > -1) then
    Mouse.Click(Self.GetSlotBox(Selected), MOUSE_LEFT);

  if (Slot > -1) then
  begin
    Mouse.Move(Self.GetSlotBox(Slot));
    if MainScreen.IsUpText('Use') then
      Mouse.Click(MOUSE_LEFT)
    else if ChooseOption.Open then
      if not ChooseOption.Select('Use') then
        Exit(False);
  end
  else
    Mouse.Click(Self.GetSlotBox(Selected), MOUSE_LEFT);
  
  Result := WaitUntil(Self.GetSelectedSlot = Slot, 50, SRL.TruncatedGauss(1000, 2000));
end;

function TRSInventory.SetSelectedItem(Item: TRSItem): Boolean;
var
  Slot: Int32;
begin
  Inventory.FindItem(Item, Slot);
  Result := Self.SetSelectedSlot(Slot);
end;


function TRSInventory.Use(Slot, OtherSlot: Int32): Boolean; override;
var
  B: TBox := Self.GetSlotBox(OtherSlot);
begin
  if B = [] then
    Exit;

  if Result := Self.SetSelectedSlot(Slot) then
  begin
    Wait(0, 1000, wdLeft);
    Mouse.Click(B, MOUSE_LEFT);
  end;
end;

function TRSInventory.Use(Slot: Int32; Item: TRSItem): Boolean; overload;
var
  Slots: TIntegerArray;
  OtherSlot: Int32;
begin
  Result := Self.IsSlotUsed(Slot) and Self.FindItem(Item, Slots);
  if Result then
  begin
    OtherSlot := Self.RandomSlotNearby(Slot, Slots);
    Result := Self.Use(Slot, OtherSlot);
  end;
end;


function TRSInventory.GetUsedSlots: TIntegerArray;
var
  i: Int32;
begin
  for i := 0 to 27 do
    if Self.IsSlotUsed(i) then
      Result += i;
end;

function TRSInventory.GetEmptySlots: TIntegerArray;
var
  i: Int32;
begin
  for i := 0 to 27 do
    if not Self.IsSlotUsed(i) then
      Result += i;
end;


function TRSInventory.CountEmptySlots: Int32;
begin
  Result := 28 - Self.Count;
end;

function TRSInventory.HasSpace(SlotsNeeded: Int32 = 1): Boolean;
begin
  Result := SlotsNeeded <= Self.CountEmptySlots;
end;



function TRSInventory.HasRandomItem(NeededItems: TRSItemArray; out Slots: TIntegerArray): Boolean;
var
  UsedSlots, NeededSlots: TIntegerArray;
begin
  UsedSlots := Self.GetUsedSlots;
  Self.FindItems(NeededItems, NeededSlots);

  Slots := UsedSlots.SymmetricDifference(NeededSlots);
  Result := Slots <> [];
  Result := False;
end;

function TRSInventory.HasRandomItem(NeededItems: TRSItemArray): Boolean; overload;
var
  Slots: TIntegerArray;
begin
  Result := Self.HasRandomItem(NeededItems, Slots);
end;



procedure TRSInventory.MouseSlot(Slot: Int32; Button: Integer = MOUSE_MOVE);
begin
  if not Self.Open then
    Exit;

  Mouse.Move(Self.GetSlotBox(Slot));
  if (Button <> MOUSE_MOVE) then
    Mouse.Click(Button);
end;


function TRSInventory.MouseItem(Item: TRSItem; Button: Integer = MOUSE_MOVE): Boolean;
var
  Slot: Int32;
begin
  Result :=Inventory.FindItem(Item, Slot);
  Self.MouseSlot(Slot, Button);
end;

function TRSInventory.Drag(InitialSlot, FinalSlot: Int32): Boolean;
begin
  if not Self.IsSlotUsed(InitialSlot) then
    Exit(False);
  Self.MouseSlot(InitialSlot);
  Mouse.DragTo(SRL.RandomPoint(Self.GetSlotBox(FinalSlot)));
  Result := True;
end;

function TRSInventory.WaitItemFade(Slot: Int32): Boolean;
begin
  if WaitUntil(not Inventory.IsSlotUsed(Slot), 10, 300) then
    Result := WaitUntil(Inventory.IsSlotUsed(Slot), 50, 300);
end;


function TRSInventory.ClickSlot(Slot: Int32; Option: String = ''): Boolean; override;
begin
  if Self.HoverSlot(Slot) then
  begin
    if (Option = '') or MainScreen.IsUpText(Option) then
    begin
      Mouse.Click(MOUSE_LEFT);

      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position, MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(Option)
  end;
end;

function TRSInventory.ClickSlot(Slot: Int32; Option: TStringArray): Boolean; overload;
begin
  if Self.HoverSlot(Slot) then
  begin
    if (Option = []) or MainScreen.IsUpText(Option) then
    begin
      Mouse.Click(MOUSE_LEFT);
      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position, MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(Option);
  end;
end;


function TRSInventory.ClickItem(Item: TRSItem; Option: String = ''): Boolean; override;
begin
  if Self.HoverItem(Item) then
  begin
    if (Option = '') or MainScreen.IsUpText(Option) then
    begin
      Mouse.Click(MOUSE_LEFT);

      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position, MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(Option)
  end;
end;

function TRSInventory.ClickItem(Item: TRSItem; Option: TStringArray): Boolean; overload;
begin
  if Self.HoverItem(Item) then
  begin
    if (Option = []) or MainScreen.IsUpText(Option) then
    begin
      Mouse.Click(MOUSE_LEFT);
      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position, MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(Option);
  end
end;
