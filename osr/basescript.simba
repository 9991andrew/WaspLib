(*
BaseScript
==========
BaseScript and it's derivants are template records to be used in scripts.
They also contain template functions and procedures that can be used as is or overriden with
"inherited" to add functionality.
*)

{$DEFINE WL_BASESCRIPT_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
const WELCOME_SENTENCES
~~~~~~~~~~~~~~~~~~~~~~~
  Global WELCOME_SENTENCES constant used to store advertising senteces to display in the progress report.
*)
const
  WELCOME_SENTENCES: TStringArray = [
    'Join WaspScripts today!',
    'Road to max cape with',
    'No time to play legit?',
    'What are you waiting for?',
    'The best public color bots',
    'Learn how to make scripts at',
    'Get that 99 with',
    'Daily bans is not normal,',
    'Everything is open source',
    'Imagine AHK on steroids',
    'Unleash the power of Simba'
  ];

(*
type TBaseScript
~~~~~~~~~~~~~~~~
Record that holds common variables for most basic scripts.
The variables that are part of **TBaseScript** are the following:
 - **Name**: String that holds the script name to be displayed in the log and on screen progress report.
 - **Version**: String that holds the script version to be displayed in the log and on screen progress report.
 - **Self.IsSetup**: Boolean that let us know if the our **TBaseScript** already set it's variables up.
 - **TimeRunning**: TStopwatch variable that holds how long our **TBaseScript** has been running.
 - **StatsDebugTick**: Integer variabled used to register the last tick we printed a progress report.
 - **Action**: String variable used to print in Simba output or on screen progress report the current action the script is performing.
 - **PreviousAction**: String variable used to register the previous action. This is used to avoid spamming the logs.
 - **ExtraInfo**: String variable we can use to print extra information in Simba output or in on screen progress report. E.G. current state of variables.
 - **ActionProfit**: Integer that holds information on how much profit (positive or negative) our script makes per action performed.
 - **TotalActions**: Integer used to count how many actions our script has done.
 - **TotalProfit**:  Integer that holds the total profit our script has done.
 - **TimeLimit**: Integer variable to set a time limit for our script in milliseconds before ending the current script.
 - **ActionLimit**: Integer variable to set total action limit before ending the script.

TBaseScript can be used as is or inherited from to extend it's functionality.

Example
-------
  TBaseWalkerScript = record(TBaseScript)
    RSW: TRSWalker;
  end;
*)
type
  TBaseScript = record(TSRLBaseRecord)
    Revision: String;
    Welcome: String;

    TimeRunning: TStopwatch;
    StatsDebugTick: UInt64;
    ReportTimer: TTimer;

    Action: String;
    PreviousAction: String;

    ExtraInfo: String;

    ActionProfit: Int32;
    TotalActions: Int32;
    TotalProfit: Int32;

    TimeLimit: Int64;
    ActionLimit: Int32;

    IsSetup: Boolean;
  end;

{$H-}
(*
BaseScript.ShouldHandlePaint()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseScript.ShouldHandlePaint(): Boolean;

Wrapper function used internally to decide if we should handle TWaspProgressReport.
*)
function TBaseScript.ShouldHandlePaint(): Boolean;
begin
  Result := RemoteInputEnabled and ProgressReportEnabled and not ScriptDebugEnabled;
end;

(*
BaseScript.ShouldDrawPaint()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseScript.ShouldDrawPaint(): Boolean;

Wrapper function used internally to decide if we should call TWaspProgressReport.Update().
To avoid unnecessary redraw, when Self.Action and Self.PreviousAction are the same, we do not redraw.
*)
function TBaseScript.ShouldDrawPaint(): Boolean;
begin
  Result := not ProgressReport.IsSetup or
            (Self.ShouldHandlePaint() and (Self.Action <> Self.PreviousAction));
end;

(*
BaseScript.PrintReport()
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseScript.PrintReport(Sender: TObject);

Internal method to print our progress report ("proggie").
This is run on a timer setup by TBaseScript and also handles drawing the HUD progress report if it is enabled.
It also is responsible for debugging the script with SRL.Debug() if we enable it.
*)
procedure TBaseScript.PrintReport(Sender: TObject);
var
  Str: String;
  ElapsedTime: UInt64;
  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  MousePos: TPoint;
  PaintDisabled: Boolean;
  {$ENDIF}
begin
  try
    {$IFNDEF SRL_DISABLE_REMOTEINPUT}
    if Self.ShouldHandlePaint() then
    begin
      EIOS_GetRealMousePosition(RSClient.RemoteInput.EIOS, MousePos.X, MousePos.Y);
      PaintDisabled := PointInBox(MousePos, DisableHoverBox);
      if PaintDisabled then
      begin
        ProgressReport.ClientBMP.Clear;
        ProgressReport.IsSetup := False;

        ProgressReport.ClientBMP.DrawBoxFilled(Chat.GetDisplayNameBox(), $819DAC);
        ProgressReport.DrawDisableButton('Show');
      end;
    end;
    {$ENDIF}

    Self.TotalProfit := (Self.TotalActions * Self.ActionProfit) - TotalConsumableCost;

    Self.DebugLn(Self.Action, Self.ExtraInfo);
    ElapsedTime := Self.TimeRunning.ElapsedTime();

    EnergyLevel := Max(Power(1 + (24 - ElapsedTime) / ONE_DAY, Sqr(Endurance+0.75*Patience+0.25)*1.33), 0);

    ProgressTitle := Self.Name;
    PadLength := ProgressTitle.Len + 6;

    ProgressTitle    := PadL((' ' + ProgressTitle + ' '), 10, ' ');
    ProgressTitle    := PadR(ProgressTitle, 90 - PadLength, ' ');

    PadLength := ProgressTitle.Len();

    ProgressArray := [];
    ProgressArray += ProgressTitle;

    Str := ' Action        : ' + Self.Action;

    ProgressArray += PadR((' Action        : ' + Self.Action), Round(PadLength/2), ' ') +
                           ' Runtime       : ' + SRL.MsToTime(ElapsedTime, Time_Short);

    if (Self.ActionLimit = -1) then
      ProgressArray += PadR((' Total Actions : ' + ToStr(Self.TotalActions)), Round(PadLength/2), ' ') +
                             ' Actions/Hour  : ' + ToStr(NumberPerHour(Self.TotalActions, ElapsedTime))
    else
      ProgressArray += PadR((' Total Actions : ' + ToStr(Self.TotalActions) + '/' + ToStr(Self.ActionLimit)), Round(PadLength/2), ' ') +
                             ' Actions/Hour  : ' + ToStr(NumberPerHour(Self.TotalActions, ElapsedTime));

    ProgressArray += PadR((' Total Profit  : ' + SRL.FormatNumber(Self.TotalProfit)), Round(PadLength/2), ' ') +
                           ' Profit/Hour   : ' + SRL.FormatNumber(NumberPerHour(Self.TotalProfit, ElapsedTime));

    ProgressArray += ' ' + Self.Welcome + ' waspscripts.com';

    ProgressArray += Self.ExtraInfo;

    {$IFNDEF SRL_DISABLE_REMOTEINPUT}
    if not PaintDisabled and Self.ShouldDrawPaint() then
        ProgressReport.Update(ProgressArray);
    {$ENDIF}

    for Str in ProgressArray do
      Writeln(Str);

    Self.PreviousAction := Self.Action;
    Self.StatsDebugTick := GetTickCount;


    if ScriptDebugEnabled then
      SRL.Debug(RSClient.Image);
  except
    WriteLn(GetExceptionMessage());
  end;
end;
{$H+}

(*
BaseScript.StartReportTimer()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseScript.StartReportTimer();

Internal method used to start the timer in which TBaseScript.PrintReport() runs on.
*)
procedure TBaseScript.StartReportTimer();
begin
  Self.ReportTimer.Init(nil);
  Self.ReportTimer.SetInterval(1000);
  Self.ReportTimer.SetOnTimer(@Self.PrintReport);
  Self.ReportTimer.SetEnabled(True);
  addOnTerminate(@Self.ReportTimer.Free);
end;

(*
BaseScript.Init()
~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseScript.Init(MaxActions: Int32; MaxTime: Int64);

Method used to setup the variables of TBaseScript.
If **MaxActions** and/or **MaxTime** -1 that will make
`TBaseScript.ShouldStop() = True` when `TBaseScript.TotalActions = MaxAtions` or
`TBaseScript.TimeRunning.ElapsedTime = MaxTime`.

Can be used as is or overriden to do additional tasks.

Example
-------

  var Script: TBaseScript;

  begin
    Script.Init(-1, 2 * ONE_DAY);
  end;
*)
procedure TBaseScript.Init(MaxActions: Int32; MaxTime: Int64);
var
  ScriptStr: String;
begin
  AddOnTerminate(@SaveScreenshot);

  TotalConsumableCost := 0;
  Self.DisableDebugging := True;
  {$IFNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '1'}{$ENDIF}
  Self.Revision := {$macro SCRIPT_REVISION};

  GenerateLog(Self.Name);
  ScriptStr := ToStr(Self);
  ScriptStr := ScriptStr.Replace('{', '').Replace(', ', ',' + STR_NEW_LINE).Replace('}', '');
  ScriptStr += STR_NEW_LINE + STR_NEW_LINE;
  WriteFileContents(WL.LogFile, ScriptStr, True);

  Self.Welcome := WELCOME_SENTENCES.Random;

  Self.ActionLimit := -1;
  if MaxActions > -1 then
    Self.ActionLimit := Self.TotalActions + MaxActions;

  Self.TimeRunning.Start;
  PTimeRunning := @Self.TimeRunning;

  if MaxTime > -1 then
    Self.TimeLimit := Self.TimeRunning.ElapsedTime + MaxTime
  else
    Self.TimeLimit := -1;


  WL.Activity.Init(260000);

  if Self.IsSetup then Exit;
  Self.IsSetup := True;

  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  if RemoteInputEnabled then
  begin
    RSClient.RemoteInput.Setup;
    if ProgressReportEnabled then
      ProgressReport.Setup;
  end;
  {$ENDIF}

  Antiban.Setup;

  Sync(@Self.StartReportTimer);
  //override me to add more stuff.
end;

(*
BaseScript.DoAntiban()
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseScript.DoAntiban(CheckBreaks: Boolean = True; CheckSleeps: Boolean = True): Boolean;

Method used to call Antiban.DoAntiban(CheckBreaks, CheckSleeps).

Example
-------

  Script.DoAntiban();
*)
function TBaseScript.DoAntiban(CheckBreaks: Boolean = True; CheckSleeps: Boolean = True): Boolean;
begin
  Antiban.DismissRandom();
  Self.TimeRunning.Pause();
  WL.Activity.Pause();

  try
    Result := Antiban.DoAntiban(CheckBreaks, CheckSleeps);
  finally
    Self.TimeRunning.Resume();
    WL.Activity.Resume();

    if not RSClient.IsLoggedIn() then
      Login.LoginPlayer();
  end;
end;

(*
BaseScript.ShouldStop()
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseScript.ShouldStop(): Boolean;

Method used to check if we reached the TBaseScript goals and it's time to stop it.

.. note:: This doesn't stop the script. This simply returns true/false when called and it's up to you to stop the script.

Example
-------

  if Script.ShouldStop() then
    TerminateScript();
*)
function TBaseScript.ShouldStop(): Boolean;
begin
  Result := ((Self.ActionLimit > -1) and (Self.TotalActions >= Self.ActionLimit)) or
            ((Self.TimeLimit > -1) and (Self.TimeRunning.ElapsedTime >= Self.TimeLimit));

  if Result then
    SaveScreenshot('ScriptEnding/shouldstop' + Self.Name, True);
end;

(*
type TBaseWalkerScript
~~~~~~~~~~~~~~~~~~~~~~
Record that holds common variables for most scripts that use walker.
This record extends **TBaseScript** and has all of it's variables too.
The extra variables that are part of **TBaseWalkerScript** are:
 - **RSW**: TRSWalker variable that holds our walker, for more information check the following page: https://ollydev.github.io/SRL-Development/walker.html?highlight=walker
*)
type
  TBaseWalkerScript = record(TBaseScript)
    RSW: TRSWalker;
  end;

(*
BaseWalkerScript.Init()
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseWalkerScript.Init(MaxActions: Int32; MaxTime: Int64); override;

Method used to setup the variables of TBaseScript.
If **MaxActions** and **MaxTime** serve the same purpose of TBaseScript.Init().

Can be used as is or overriden to do additional tasks.
*)
procedure TBaseWalkerScript.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  inherited;

  Self.RSW.OnWalkingEvent := @WalkerTasks;
end;

(*
type TBaseBankScript
~~~~~~~~~~~~~~~~~~~~
Record that holds common variables for most scripts that use a bank.
This record inherits from **TBaseWalkerScript** and which inherits from **TBaseScript's**.
The extra variables that are part of **TBaseBankScript** are:
 - **Self.ScriptBank**: PRSObject pointer that points to our current bank TRSObject, for more info check: https://torwent.github.io/WaspLib/waspobject.html#type-trsobject
 - **Self.ScriptBanker**: PRSNPC pointer that points to our current banker TRSNPC, for more info check: https://torwent.github.io/WaspLib/waspobject.html#type-trsmmdot
 - **BankTab**: Integer variable used to cache the current bank tab that contains items for our script.
 - **ItemLeftAmount**: Integer variable used to cache items left to make/build/whatever to pre-hover the bank.
 - **HoveringBank**: Boolean variable that holds information if we are currently already hovering the bank. This is so we don't try to rehover the bank and don't need to check the uptext if we already are doing it.
 - **BankEmpty**: Boolean variable that that is set to true when we ran out of an item our script needs. This is used so next time we need to fetch the item we check the deposit box instead.
 - **CollectEmpty**: Boolean variable that that is set to true when the collect box is empty. Normally, if this and **BankEmpty** are true the script ends.
 - **CollectTimer**: TCountDown variable used to reset **CollectEmpty** after a couple of minutes.
*)
type
  TBaseBankScript = record(TBaseWalkerScript)
    ScriptBank:   TRSObject;
    ScriptBanker: TRSNPC;

    BankTab: Int32;

    ItemLeftAmount: Int32;
    HoveringBank: Boolean;
    BankEmpty: Boolean;       //Used to cache if Bank doesn't have more of a certain item.
    CollectEmpty: Boolean;    //Used to cache if CollectBox doesn't have more of a certain item.
    CollectTimer: TCountDown; //Used to reset CollectEmpty after a while.
  end;

(*
BaseBankScript.Init()
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseBankScript.Init(MaxActions: Int32; MaxTime: Int64); override;

Method used to setup the variables of TBaseScript.
If **MaxActions** and **MaxTime** serve the same purpose of TBaseScript.Init().

Can be used as is or overriden to do additional tasks.
*)
procedure TBaseBankScript.Init(MaxActions: Int32; MaxTime: Int64); override;
begin
  inherited;

  Self.BankTab := -1;
  Self.CollectTimer.Init(600000);

  //override me to add more stuff.
end;

(*
BaseBankScript.SetupWLBanks()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseBankScript.SetupWLBanks();

Method used to setup the **CurrentBank** of TBaseBankScript.
**CurrentBankLocation** has to be set beforehand by default it uses the grand exchange.

Example
-------

  CurrentBankLocation := EWLBankLocation.ARDOUGNE_SOUTH;
  Script.SetupWLBanks();
*)
procedure TBaseBankScript.SetupWLBanks();
begin
  case CurrentBankLocation of
    EWLBankLocation.AL_KHARID:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.AlKharidBank;
      end;

    EWLBankLocation.ARDOUGNE_NORTH:
      begin
		    Self.RSW.Setup('x5723y3038');
		    Self.ScriptBank := RSObjects.ArdougneNorthBank;
      end;

    EWLBankLocation.ARDOUGNE_SOUTH:
      begin
		    Self.RSW.Setup('x5723y3038');
		    Self.ScriptBank := RSObjects.ArdougneSouthBank;
      end;

    EWLBankLocation.CASTLE_WARS:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.CastleWarsBank;
      end;

    EWLBankLocation.CATHERBY:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.CatherbyBank;
      end;

    EWLBankLocation.CRAFT_GUILD:
      begin
		    Self.RSW.Setup('x6953y3166');
		    Self.ScriptBank := RSObjects.CraftGuildBank;
      end;

    EWLBankLocation.DRAYNOR:
      begin
		    Self.RSW.Setup('x7617y3214');
		    Self.ScriptBank := RSObjects.DraynorBank;
      end;

    EWLBankLocation.EDGEVILLE:
      begin
		    Self.RSW.Setup('x7536y2363');
		    Self.ScriptBank := RSObjects.EdgevilleBank;
      end;

    EWLBankLocation.FALADOR_EAST:
      begin
		    Self.RSW.Setup('x7051y2796');
		    Self.ScriptBank := RSObjects.FaladorEastBank;
      end;

    EWLBankLocation.FALADOR_WEST:
      begin
		    Self.RSW.Setup('x7051y2796');
		    Self.ScriptBank := RSObjects.FaladorWestBank;
      end;

    EWLBankLocation.GRAND_EXCHANGE:
      begin
		    Self.RSW.Setup('x7789y2306');
		    Self.ScriptBank := RSObjects.GEBank;
      end;

    EWLBankLocation.LUNAR_ISLAND:
      begin
		    Self.RSW.Setup('x4127y1027');
		    Self.ScriptBank := RSObjects.LunarBank;
      end;

    EWLBankLocation.MINING_GUILD:
      begin
		    Self.RSW.Setup('x6291y497');
		    Self.ScriptBank := RSObjects.MiningGuildBank;
      end;

    EWLBankLocation.PRIFDDINNAS:
      begin
		    Self.RSW.Setup('x3583y1905');
		    Self.ScriptBank := RSObjects.PrifddinasBank;
      end;

    EWLBankLocation.SEERS:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.SeersBank;
      end;

    EWLBankLocation.TZHAAR:
      begin
		    Self.RSW.Setup('x4333y310');
		    Self.ScriptBank := RSObjects.TzhaarBank;
      end;

    EWLBankLocation.VARROCK_EAST:
      begin
		    Self.RSW.Setup('x7536y2363');
		    Self.ScriptBank := RSObjects.VarrockEastBank;
      end;

    EWLBankLocation.VARROCK_WEST:
      begin
		    Self.RSW.Setup('x7789y2306');
		    Self.ScriptBank := RSObjects.VarrockWestBank;
      end;

    EWLBankLocation.WINTERTODT:
      begin
		    Self.RSW.Setup('x1549y224');
		    Self.ScriptBank := RSObjects.WintertodtBank;
      end;
  end;
end;

(*
BaseBankScript.Terminate()
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseBankScript.Terminate(): Boolean;

Helper method to be called at the end of the life cycle of a TBaseBankScript.
By default it simply attempts to open the TBaseBankScript.CurrentBank but it's meant to
be overriden and inherited or completely be remade for proper termination of the TBaseBankScript.

This is likely required if you want to chain several scripts together.

.. note:: Consider taking a look at script_chainer.simba: https://github.com/Torwent/wasp-mini/blob/master/script_chainer.simba

Example
-------

  Script1.Init();
  repeat
    //do stuff...
  until Script1.ShouldStop();

  Script1.Terminate();

  Script2.Init();
  repeat
    //do stuff...
  until Script2.ShouldStop();
*)
function TBaseBankScript.Terminate(): Boolean;
begin
  for 0 to 5 do
  begin
    if MainScreen.HasInterface then
    begin
      if Result := Bank.IsOpen then Break
      else MainScreen.CloseInterface;
    end;

    if Result := Bank.WalkOpen(Self.ScriptBank) then
      Break;
  end;
  //override me to add more stuff.
end;

(*
BaseBankScript.CountItemsLeft()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TBaseBankScript.CountItemsLeft(Item: TRSItem);

Set's TBaseBankScript.ItemLeftAmount to be used by TBaseBankScript.ShouldHover().

.. note:: For some scripts this might need to be overriten and rewritten.
*)
procedure TBaseBankScript.CountItemsLeft(Item: TRSItem);
begin
  Self.ItemLeftAmount := Inventory.CountItem(Item);
end;

(*
BaseBankScript.ShouldHover()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseBankScript.ShouldHover(): Boolean;

Method used to decide if we should pre-hover the bank or not.
It uses the amount of items left in the inventory stored by TBaseBankScript.CountItemsLeft()
in TBaseBankScript.ItemLeftAmount() along with the player BioHash to decide if we want to
pre-hover the bank.

.. note:: This function simply returns true/false and doesn't actualy hover the bank.

Example
-------

  if Script.ShouldHover() then
    Script.Hover();
*)
function TBaseBankScript.ShouldHover(): Boolean;
begin
  Result := ((Self.ItemLeftAmount = 1) and SRL.Dice(10 + (90 * BioHash))) or
            ((Self.ItemLeftAmount = 2) and SRL.Dice(5 + (75 * BioHash))) or
            ((Self.ItemLeftAmount = 3) and SRL.Dice(5 + (60 * BioHash))) or
            ((Self.ItemLeftAmount = 4) and SRL.Dice(45 * BioHash)) or
            ((Self.ItemLeftAmount = 5) and SRL.Dice(30 * BioHash)) or
            ((Self.ItemLeftAmount = 6) and SRL.Dice(10 * BioHash)) or
            SRL.Dice(5 * BioHash);
end;

(*
BaseBankScript.Hover()
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TBaseBankScript.Hover(): Boolean;

Moves the mouse to the TBaseBankScript.CurrentBank RSObject.

Example
-------

  BankScript.Hover();
*)
function TBaseBankScript.Hover(): Boolean;
begin
  Result := Self.HoveringBank := Bank.Hover(Self.ScriptBank);
end;


procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  MM2MS.SetupZoom;
  if (Bitmap = RSClient.Image) then
    Bitmap.Clear;

  inherited(Bitmap);
end;




