(*
Minimap
=======
Methods to handle Minimap. Extends SRL's Minimap.
*)


{$DEFINE WL_MINIMAP_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
Minimap.WaitMoving
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSMinimap.WaitMoving(DoAntiban: Boolean = True);

Gives **Minimap.WaitPlayerMoving** the ability to perform antiban tasks while moving.

Example
-------

  Minimap.WaitMoving;
*)
procedure TRSMinimap.WaitMoving(DoAntiban: Boolean = True);
var
  Timeout, Countdown: TCountDown;
  Moving: Boolean;
begin
  Timeout.Init(Round(SRL.TruncatedGauss(20000, 25000)));

  Countdown.Init(Round(SRL.TruncatedGauss(500, 1500)));
  Countdown.Timeout := GetTickCount + Round(SRL.TruncatedGauss(2500, 3500));

  repeat
    //Self.CheckRunEnergy();

    if DoAntiban then
      Antiban.WaitMovingTasks;

    Moving := Minimap.IsPlayerMoving;

    if Moving then Countdown.Restart;

  until Timeout.IsFinished or Countdown.IsFinished or not Minimap.HasFlag or not Moving;
end;

(*
Minimap.GetOrbLevel
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMinimap.GetOrbLevel(Orb: TCircle): Int32; override;

Override to fix SRL's **Minimap.GetOrbLevel**. Should add it to SRL in the future.

Example
-------

  WriteLn Minimap.GetOrbLevel(Minimap.GetHPCircle);
*)
function TRSMinimap.GetOrbLevel(Orb: TCircle): Int32; override;
begin
  Result := OCR.RecognizeNumber([Orb.X-35, Orb.Y-1, Orb.X-15, Orb.Y+9], TOCRShadowRule.Create(), RS_FONT_PLAIN_11);
end;

function TRSMinimap.GetSpecLevel: Int32;
begin
  Result := Self.GetOrbLevel(Self.GetSpecAttackCircle);
end;

function TRSMinimap.IsSpecEnabled: Boolean;
begin
  Result := SRL.CountColor(CTS2(8682326, 16, 0.03, 1.15), Self.GetSpecAttackCircle.Bounds) > 1;
end;

function TRSMinimap.IsSpecWeapon: Boolean;
begin
  Result := ((SRL.CountColor(CTS2(8480786, 19, 0.05, 1.36), Self.GetSpecAttackCircle.Bounds) > 0) or
            Self.IsSpecEnabled);
end;

function TRSMinimap.EnableSpec(MinSpec: Int32): Boolean;
var
  T: UInt64;
begin
  if not Self.IsSpecWeapon or (Self.GetSpecLevel < MinSpec) then
    Exit(False);

  Result := Self.IsSpecEnabled;
  if not Result then
  begin
    Mouse.Click(Self.GetSpecAttackCircle, MOUSE_LEFT);

    T := GetTickCount() + Round(SRL.TruncatedGauss(1000, 2000));
    while (T > GetTickCount()) do
    begin
      if Self.IsSpecEnabled() then
        Exit(True);

      Wait(0, 1000, wdLeft);
    end;
  end;
end;

function TRSMinimap.DisableSpec: Boolean;
var
  T: UInt64;
begin
  Result := not Self.IsSpecEnabled;

  if Result then
  begin
    Mouse.Click(Self.GetSpecAttackCircle, MOUSE_LEFT);

    T := GetTickCount() + Round(SRL.TruncatedGauss(1000, 2000));
    while (T > GetTickCount()) do
    begin
      if not Self.IsSpecEnabled() then
        Exit(True);

      Wait(0, 1000, wdLeft);
    end;
  end;
end;


function TRSMinimap.HasStamina: Boolean;
begin
  Result := SRL.CountColor(CTS0(2709951, 38), Self.GetRunCircle.Bounds) > 0;
end;



function TRSMinimap.GetTileMS(Loc: TPoint; TileVector: Vector3; Offset: TPoint = [0, 0]; Angle: Single = $FFFF): TRectangle;
begin
  if not Self.IsPointOn(Loc) then
    Exit;

  if (Angle = $FFFF) then Angle := Self.GetCompassAngle(False);

  Result := Self.VecToMSRect(
                  Vec3(Loc.x - Offset.X, Loc.y - Offset.Y, TileVector.Z),
                  TileVector.X, TileVector.Y, Angle);
end;

function TRSMinimap.GetTileMS(Loc: TPoint; Z: Double = 0): TRectangle; overload;
begin
  Result := Self.GetTileMS(Loc, [1, 1, Z]);
end;



function TRSMinimap.GetTilePoly(Loc: TPoint; TileVector: Vector3; Offset: TPoint = [0, 0]; Angle: Single = $FFFF): TPoly;
var
  Rect: TRectangle;
begin
  if (Angle = $FFFF) then Angle := Self.GetCompassAngle(False);

  Rect := Self.GetTileMS(Loc, [TileVector.Y, TileVector.X, 0], Offset, Angle);

  if TileVector.Z = 0 then
    Result := Rect.ToTPA
  else
    Result := Rect.GetPoly(Self.GetTileMS(Loc, TileVector, Offset, Angle));
end;

function TRSMinimap.GetTilePoly(Loc: TPoint; Z: Double = 0): TPoly; overload;
begin
  Result := Self.GetTilePoly(Loc, [1, 1, Z]);
end;



function TRSMinimap.GetTileArrayMS(LocArray: TPointArray; TileVector: Vector3; Offset: TPoint = [0, 0]; Angle: Single = $FFFF): TRectArray;
var
  Loc: TPoint;
begin
  if (Angle = $FFFF) then Angle := Self.GetCompassAngle(False);

  for Loc in LocArray.Sorted(Self.Center) do
    Result += Self.GetTileMS(Loc, TileVector, Offset, Angle);
end;

function TRSMinimap.GetTileArrayMS(LocArray: TPointArray; Z: Double = 0): TRectArray; overload;
begin
  Result := Self.GetTileArrayMS(LocArray, [1, 1, Z]);
end;



function TRSMinimap.GetTileArrayPolys(LocArray: TPointArray; TileVector: Vector3; Offset: TPoint = [0, 0]; Angle: Single = $FFFF): TPolyArray;
var
  Loc: TPoint;
begin
  if (Angle = $FFFF) then Angle := Self.GetCompassAngle(False);

  for Loc in LocArray do
    Result += Self.GetTilePoly(Loc, TileVector, Offset, Angle);
end;

function TRSMinimap.GetTileArrayPolys(LocArray: TPointArray; Z: Double = 0): TPolyArray; overload;
begin
  Result := Self.GetTileArrayPolys(LocArray, [1, 1, Z]);
end;



//Get a radius in the minimap
function TRSMinimap.GetRadius(Loc: TPoint; Offset: TPoint; Radius: Int32): TCircle;
begin
  if (Self.Center.DistanceTo((Loc - Offset)) - Radius) > 80 then
    Exit; //This means it's impossible the result will be in the minimap.

  Loc := Loc - Offset;
  Result := [Loc.X, Loc.Y, Radius * 4];
end;

function TRSMinimap.GetRadius(Loc: TPoint; Radius: Int32): TCircle; overload;
begin
  Result := Self.GetRadius(Loc, [0, 0], Radius);
end;

//Get several radius in the minimap
function TRSMinimap.GetRadiuses(Locs: TPointArray; Offset: TPoint; Radius: Int32): TCircleArray;
var
  Loc: TPoint;
begin
  for Loc in Locs do
    Result += Self.GetRadius(Loc, Offset, Radius);
end;

function TRSMinimap.GetRadiuses(Loc: TPointArray; Radius: Int32): TCircleArray; overload;
begin
  Result := Self.GetRadiuses(Loc, [0, 0], Radius);
end;



//Get the first dot within a radius.
function TRSMinimap.GetDotInRadius(Dot: ERSMinimapDot; Loc, Offset: TPoint; Radius: Int32): TPoint;
var
  i: Int32;
  Dots: TPointArray;
  Circle: TCircle;
begin
  Dots := Self.GetDots(Dot);

  Dots.Sort(Self.Center);

  Circle := Self.GetRadius(Loc, Offset, Radius);
  for i := 0 to High(Dots) do
    if SRL.PointInCircle(Dots[i], Circle.Mean, Circle.Radius) then
      Exit(Dots[i]);
end;

function TRSMinimap.GetDotInRadius(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32): TPoint; overload;
begin
  Result := Self.GetDotInRadius(Dot, Loc, [0, 0], Radius);
end;

//Get all first dots withing several radius.
function TRSMinimap.GetDotInRadius(Dot: ERSMinimapDot; Locs: TPointArray; Offset: TPoint; Radius: Int32): TPointArray; overload;
var
  Loc: TPoint;
begin
  for Loc in Locs do
    Result += Self.GetDotInRadius(Dot, Loc, Offset, Radius);
end;

function TRSMinimap.GetDotInRadius(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32): TPointArray; overload;
begin
  Result := Self.GetDotInRadius(Dot, Locs, [0, 0], Radius);
end;



//Get all dots within a radius.
function TRSMinimap.GetDotArrayInRadius(Dot: ERSMinimapDot; Loc, Offset: TPoint; Radius: Int32): TPointArray;
var
  i: Int32;
  Dots: TPointArray;
  Circle: TCircle;
begin
  Dots := Self.GetDots(Dot);

  Dots.Sort(Self.Center);

  Circle := Self.GetRadius(Loc, Offset, Radius);
  for i := 0 to High(Dots) do
    if SRL.PointInCircle(Dots[i], Circle.Mean, Circle.Radius) then
      Result += Dots[i];
end;

function TRSMinimap.GetDotArrayInRadius(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32): TPointArray; overload;
begin
  Result := Self.GetDotArrayInRadius(Dot, Loc, [0, 0], Radius);
end;

//Get all dots within several radius.
function TRSMinimap.GetDotArrayInRadiuses(Dot: ERSMinimapDot; Locs: TPointArray; Offset: TPoint; Radius: Int32): TPointArray;
var
  i: Int32;
  ATPA: T2DPointArray;
begin
  for i := 0 to High(Locs) do
    ATPA += Self.GetDotArrayInRadius(Dot, Locs[i], Offset, Radius);

  Result := ATPA.Merge;

  Result.Sort(Self.Center);
end;

function TRSMinimap.GetDotArrayInRadiuses(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32): TPointArray; overload;
begin
  Result := Self.GetDotArrayInRadiuses(Dot, Locs, [0, 0], Radius);
end;


//Get the closest dot tile within a radius
function TRSMinimap.GetDotTileMS(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32; TileVector: Vector3; Offset, RadiusOffset: TPoint = [0, 0]): TRectangle;
begin
  Result := Self.GetTileMS(
              Self.GetDotInRadius(Dot, Loc, RadiusOffset, Radius),
              TileVector, Offset, $FFFF);
end;

function TRSMinimap.GetDotTileMS(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32; Z: Double = 0): TRectangle; overload;
begin
  Result := Self.GetDotTileMS(Dot, Loc, Radius, [1, 1, Z]);
end;


//Get the closest dot tile within several radius
function TRSMinimap.GetDotTileMS(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32; TileVector: Vector3; Offset, RadiusOffset: TPoint = [0, 0]): TRectArray; overload;
begin
  Result := Self.GetTileArrayMS(Self.GetDotInRadius(Dot, Locs, RadiusOffset, Radius),
                                TileVector, Offset, $FFFF);
end;

function TRSMinimap.GetDotTileMS(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32; Z: Double = 0): TRectArray; overload;
begin
  Result := Self.GetDotTileMS(Dot, Locs, Radius, [1, 1, Z]);
end;


//Get all dot tiles within a radius
function TRSMinimap.GetDotArrayTilesMS(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32; TileVector: Vector3; Offset, RadiusOffset: TPoint = [0, 0]): TRectArray;
begin
  Result := Self.GetTileArrayMS(
                  Self.GetDotArrayInRadius(Dot, Loc, RadiusOffset, Radius),
                  TileVector, Offset, $FFFF);
end;

function TRSMinimap.GetDotArrayTilesMS(Dot: ERSMinimapDot; Loc: TPoint; Radius: Int32; Z: Double = 0): TRectArray; overload;
begin
  Result := Self.GetDotArrayTilesMS(Dot, Loc, Radius, [1, 1, Z]);
end;


//Get all dot tiles within several radius
function TRSMinimap.GetDotArrayTilesMS(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32; TileVector: Vector3; Offset, RadiusOffset: TPoint = [0, 0]): TRectArray; overload;
begin
  Result := Self.GetTileArrayMS(
                  Self.GetDotArrayInRadiuses(Dot, Locs, RadiusOffset, Radius),
                  TileVector, Offset, $FFFF);
end;

function TRSMinimap.GetDotArrayTilesMS(Dot: ERSMinimapDot; Locs: TPointArray; Radius: Int32; Z: Double = 0): TRectArray; overload;
begin
  Result := Self.GetDotArrayTilesMS(Dot, Locs, Radius, [1, 1, Z]);
end;


















