(*
BaseScript
==========
BaseScript and it's derivants are template records to be used in scripts.
They also contain template functions and procedures that can be used as is or overriden with
"inherited" to add functionality.
*)

{$DEFINE WL_BASESCRIPT_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

const
  WELCOME_SENTENCES: TStringArray = [
    'Join WaspScripts today!',
    'Road to max cape with',
    'No time to play legit?',
    'What are you waiting for?',
    'The best public color bots',
    'Learn how to make scripts at',
    'Get that 99 with',
    'Daily bans is not normal,',
    'Everything is open source',
    'Imagine AHK on steroids',
    'Unleash the power of Simba'
  ];

type
(*
type TBaseScript
~~~~~~~~~~~~~~~~
Record that holds common variables for most basic scripts.
The variables that are part of **TBaseScript** are the following:
 - **Name**: String that holds the script name to be displayed in the log and on screen progress report.
 - **Version**: String that holds the script version to be displayed in the log and on screen progress report.
 - **Self.IsSetup**: Boolean that let us know if the our **TBaseScript** already set it's variables up.
 - **TimeRunning**: TStopwatch variable that holds how long our **TBaseScript** has been running.
 - **StatsDebugTick**: Integer variabled used to register the last tick we printed a progress report.
 - **Action**: String variable used to print in Simba output or on screen progress report the current action the script is performing.
 - **PreviousAction**: String variable used to register the previous action. This is used to avoid spamming the logs.
 - **ExtraInfo**: String variable we can use to print extra information in Simba output or in on screen progress report. E.G. current state of variables.
 - **ActionProfit**: Integer that holds information on how much profit (positive or negative) our script makes per action performed.
 - **TotalActions**: Integer used to count how many actions our script has done.
 - **TotalProfit**:  Integer that holds the total profit our script has done.
 - **TimeLimit**: Integer variable to set a time limit for our script in milliseconds before ending the current script.
 - **ActionLimit**: Integer variable to set total action limit before ending the script.
*)
  TBaseScript = record(TSRLBaseRecord)
    Version: String;
    Welcome: String;

    IsSetup: Boolean;

    TimeRunning: TStopwatch;
    StatsDebugTick: UInt64;

    Action: String;
    PreviousAction: String;

    ExtraInfo: String;

    ActionProfit: Int32;
    TotalActions: Int32;
    TotalProfit: Int32;

    TimeLimit: Int64;
    ActionLimit: Int32;
  end;

(*
type TBaseWalkerScript
~~~~~~~~~~~~~~~~
Record that holds common variables for most scripts that use walker.
This record inherits from **TBaseScript** and has all of it's variables too.
The extra variables that are part of **TBaseWalkerScript** are:
 - **RSW**: TRSWalker variable that holds our walker, for more information check the following page: https://ollydev.github.io/SRL-Development/walker.html?highlight=walker
*)
  TBaseWalkerScript = record(TBaseScript)
    RSW: TRSWalker;
  end;

(*
type TBaseBankScript
~~~~~~~~~~~~~~~~
Record that holds common variables for most scripts that use a bank.
This record inherits from **TBaseWalkerScript** and has all of it's variables as well as **TBaseScript's**.
The extra variables that are part of **TBaseBankScript** are:
 - **Self.ScriptBank**: PRSObject pointer that points to our current bank TRSObject, for more info check: https://torwent.github.io/WaspLib/waspobject.html#type-trsobject
 - **Self.ScriptBanker**: PRSNPC pointer that points to our current banker TRSNPC, for more info check: https://torwent.github.io/WaspLib/waspobject.html#type-trsmmdot
 - **BankTab**: Integer variable used to cache the current bank tab that contains items for our script.
 - **ItemLeftAmount**: Integer variable used to cache items left to make/build/whatever to pre-hover the bank.
 - **HoveringBank**: Boolean variable that holds information if we are currently already hovering the bank. This is so we don't try to rehover the bank and don't need to check the uptext if we already are doing it.
 - **BankEmpty**: Boolean variable that that is set to true when we ran out of an item our script needs. This is used so next time we need to fetch the item we check the deposit box instead.
 - **CollectEmpty**: Boolean variable that that is set to true when the collect box is empty. Normally, if this and **BankEmpty** are true the script ends.
 - **CollectTimer**: TCountDown variable used to reset **CollectEmpty** after a couple of minutes.
*)
  TBaseBankScript = record(TBaseWalkerScript)
    ScriptBank:   TRSObject;
    ScriptBanker: TRSNPC;

    BankTab: Int32;

    ItemLeftAmount: Int32;
    HoveringBank: Boolean;
    BankEmpty: Boolean;       //Used to cache if Bank doesn't have more of a certain item.
    CollectEmpty: Boolean;    //Used to cache if CollectBox doesn't have more of a certain item.
    CollectTimer: TCountDown; //Used to reset CollectEmpty after a while.
  end;

var
  TotalConsumableCost: Int32;

procedure TBaseScript.Init(MaxActions: Int32; MaxTime: Int64); //override me to add more stuff.
var
  ScriptStr: String;
begin
  AddOnTerminate(@SaveScreenshot);

  Self.DisableDebugging := True;
  Self.Version := ReadFileContents(IncludePath + 'WaspLib\version.txt');

  GenerateLog(Self.Name);
  ScriptStr := ToStr(Self);
  ScriptStr := ScriptStr.Replace('{', '').Replace(', ', ',' + STR_NEW_LINE).Replace('}', '');
  ScriptStr += STR_NEW_LINE + STR_NEW_LINE;
  WriteFileContents(WL.LogFile, ScriptStr, True);

  Self.Welcome := WELCOME_SENTENCES.Random;

  Self.ActionLimit := -1;
  if MaxActions > -1 then
    Self.ActionLimit := Self.TotalActions + MaxActions;

  Self.TimeRunning.Start;
  PTimeRunning := @Self.TimeRunning;

  if MaxTime > -1 then
    Self.TimeLimit := Self.TimeRunning.ElapsedTime + MaxTime
  else
    Self.TimeLimit := -1;


  WL.Activity.Init(260000);

  if Self.IsSetup then Exit;
  Self.IsSetup := True;

  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  if RemoteInputEnabled then
  begin
    RSClient.RemoteInput.Setup;
    if ProgressReportEnabled then
      ProgressReport.Setup;
  end;
  {$ENDIF}

  Antiban.Setup;

  //override me to add more stuff.
end;

function TBaseScript.DoAntiban(CheckBreaks: Boolean = True; CheckSleeps: Boolean = True): Boolean;
begin
  Antiban.DismissRandom();
  Self.TimeRunning.Pause();
  WL.Activity.Pause();

  try
    Result := Antiban.DoAntiban(CheckBreaks, CheckSleeps);
  finally
    Self.TimeRunning.Resume();
    WL.Activity.Resume();

    if not RSClient.IsLoggedIn() then
      Login.LoginPlayer();
  end;
end;

procedure TBaseScript.ProcessWhileWaiting();
var
  Str: String;
  ElapsedTime: UInt64;
begin
  Self.TotalProfit := (Self.TotalActions * Self.ActionProfit) - TotalConsumableCost;

  if (GetTickCount() - Self.StatsDebugTick) < 1000 then
    Exit;

  Self.DebugLn(Self.Action, Self.ExtraInfo);
  ElapsedTime := Self.TimeRunning.ElapsedTime();

  EnergyLevel := Max(Power(1 + (24 - ElapsedTime) / ONE_DAY, Sqr(Endurance+0.75*Patience+0.25)*1.33), 0);

  ProgressTitle := Self.Name;
  PadLength := ProgressTitle.Len + 6;

  ProgressTitle    := PadL((' ' + ProgressTitle + ' '), 10, ' ');
  ProgressTitle    := PadR(ProgressTitle, 90 - PadLength, ' ');

  PadLength := ProgressTitle.Len;

  ProgressArray := [];
  ProgressArray += ProgressTitle;

  Str := ' Action        : ' + Self.Action;

  ProgressArray += PadR((' Action        : ' + Self.Action), Round(PadLength/2), ' ') +
                         ' Runtime       : ' + SRL.MsToTime(ElapsedTime, Time_Short);

  if (Self.ActionLimit = -1) then
    ProgressArray += PadR((' Total Actions : ' + ToStr(Self.TotalActions)), Round(PadLength/2), ' ') +
                           ' Actions/Hour  : ' + ToStr(NumberPerHour(Self.TotalActions, ElapsedTime))
  else
    ProgressArray += PadR((' Total Actions : ' + ToStr(Self.TotalActions) + '/' + ToStr(Self.ActionLimit)), Round(PadLength/2), ' ') +
                           ' Actions/Hour  : ' + ToStr(NumberPerHour(Self.TotalActions, ElapsedTime));

  ProgressArray += PadR((' Total Profit  : ' + SRL.FormatNumber(Self.TotalProfit)), Round(PadLength/2), ' ') +
                         ' Profit/Hour   : ' + SRL.FormatNumber(NumberPerHour(Self.TotalProfit, ElapsedTime));

  ProgressArray += ' ' + Self.Welcome + ' waspscripts.com';

  ProgressArray += Self.ExtraInfo;

  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  if RemoteInputEnabled and ProgressReportEnabled and not ScriptDebugEnabled and
  (Self.Action <> Self.PreviousAction) then
    ProgressReport.Update(ProgressArray);
  {$ENDIF}

  for Str in ProgressArray do
    Writeln(Str);

  Self.PreviousAction := Self.Action;
  Self.StatsDebugTick := GetTickCount;


  if ScriptDebugEnabled then
    SRL.Debug(RSClient.Image);
end;

function TBaseScript.ShouldStop: Boolean;
begin
  Result := ((Self.ActionLimit > -1) and (Self.TotalActions >= Self.ActionLimit)) or
            ((Self.TimeLimit > -1) and (Self.TimeRunning.ElapsedTime >= Self.TimeLimit));

  if Result then
    SaveScreenshot('ScriptEnding/shouldstop' + Self.Name, True);
end;




procedure TBaseWalkerScript.Init(MaxActions: Int32; MaxTime: Int64); override;//override me to add more stuff.
begin
  inherited;

  Self.RSW.OnWalkingEvent := @WalkerTasks;
end;



procedure TBaseBankScript.Init(MaxActions: Int32; MaxTime: Int64); override;//override me to add more stuff.
begin
  inherited;

  Self.BankTab := -1;
  Self.CollectTimer.Init(600000);

  //override me to add more stuff.
end;

procedure TBaseBankScript.SetupWLBanks;
begin
  case CurrentBankLocation of
    EWaspBankLocation.AL_KHARID:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.AlKharidBank;
      end;

    EWaspBankLocation.ARDOUGNE_NORTH:
      begin
		    Self.RSW.Setup('x5723y3038');
		    Self.ScriptBank := RSObjects.ArdougneNorthBank;
      end;

    EWaspBankLocation.ARDOUGNE_SOUTH:
      begin
		    Self.RSW.Setup('x5723y3038');
		    Self.ScriptBank := RSObjects.ArdougneSouthBank;
      end;

    EWaspBankLocation.CASTLE_WARS:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.CastleWarsBank;
      end;

    EWaspBankLocation.CATHERBY:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.CatherbyBank;
      end;

    EWaspBankLocation.CRAFT_GUILD:
      begin
		    Self.RSW.Setup('x6953y3166');
		    Self.ScriptBank := RSObjects.CraftGuildBank;
      end;

    EWaspBankLocation.DRAYNOR:
      begin
		    Self.RSW.Setup('x7617y3214');
		    Self.ScriptBank := RSObjects.DraynorBank;
      end;

    EWaspBankLocation.EDGEVILLE:
      begin
		    Self.RSW.Setup('x7536y2363');
		    Self.ScriptBank := RSObjects.EdgevilleBank;
      end;

    EWaspBankLocation.FALADOR_EAST:
      begin
		    Self.RSW.Setup('x7051y2796');
		    Self.ScriptBank := RSObjects.FaladorEastBank;
      end;

    EWaspBankLocation.FALADOR_WEST:
      begin
		    Self.RSW.Setup('x7051y2796');
		    Self.ScriptBank := RSObjects.FaladorWestBank;
      end;

    EWaspBankLocation.GRAND_EXCHANGE:
      begin
		    Self.RSW.Setup('x7789y2306');
		    Self.ScriptBank := RSObjects.GEBank;
      end;

    EWaspBankLocation.LUNAR_ISLAND:
      begin
		    Self.RSW.Setup('x4127y1027');
		    Self.ScriptBank := RSObjects.LunarBank;
      end;

    EWaspBankLocation.MINING_GUILD:
      begin
		    Self.RSW.Setup('x6291y497');
		    Self.ScriptBank := RSObjects.MiningGuildBank;
      end;

    EWaspBankLocation.PRIFDDINNAS:
      begin
		    Self.RSW.Setup('x3583y1905');
		    Self.ScriptBank := RSObjects.PrifddinasBank;
      end;

    EWaspBankLocation.SEERS:
      begin
		    Self.RSW.Setup('mainland');
		    Self.ScriptBank := RSObjects.SeersBank;
      end;

    EWaspBankLocation.TZHAAR:
      begin
		    Self.RSW.Setup('x4333y310');
		    Self.ScriptBank := RSObjects.TzhaarBank;
      end;

    EWaspBankLocation.VARROCK_EAST:
      begin
		    Self.RSW.Setup('x7536y2363');
		    Self.ScriptBank := RSObjects.VarrockEastBank;
      end;

    EWaspBankLocation.VARROCK_WEST:
      begin
		    Self.RSW.Setup('x7789y2306');
		    Self.ScriptBank := RSObjects.VarrockWestBank;
      end;

    EWaspBankLocation.WINTERTODT:
      begin
		    Self.RSW.Setup('x1549y224');
		    Self.ScriptBank := RSObjects.WintertodtBank;
      end;
  end;
end;


function TBaseBankScript.Terminate: Boolean;//override me to add more stuff.
begin
  for 0 to 5 do
  begin
    if MainScreen.HasInterface then
    begin
      if Result := Bank.IsOpen then Break
      else MainScreen.CloseInterface;
    end;

    if Result := Bank.WalkOpen(Self.ScriptBank) then
      Break;
  end;
  //override me to add more stuff.
end;

procedure TBaseBankScript.CountItemsLeft(Item: TRSItem);
begin
  Self.ItemLeftAmount := Inventory.CountItem(Item);
end;

function TBaseBankScript.ShouldHover: Boolean;
begin
  Result := ((Self.ItemLeftAmount = 1) and SRL.Dice(10 + (90 * BioHash))) or
            ((Self.ItemLeftAmount = 2) and SRL.Dice(5 + (75 * BioHash))) or
            ((Self.ItemLeftAmount = 3) and SRL.Dice(5 + (60 * BioHash))) or
            ((Self.ItemLeftAmount = 4) and SRL.Dice(45 * BioHash)) or
            ((Self.ItemLeftAmount = 5) and SRL.Dice(30 * BioHash)) or
            ((Self.ItemLeftAmount = 6) and SRL.Dice(10 * BioHash)) or
            SRL.Dice(5 * BioHash);
end;


function TBaseBankScript.Hover: Boolean;
begin
  Result := Self.HoveringBank := Bank.Hover(Self.ScriptBank);
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  MM2MS.SetupZoom;
  if (Bitmap = RSClient.Image) then
    Bitmap.Clear;

  inherited(Bitmap);
end;




