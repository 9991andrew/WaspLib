(*
XPBar
=====
Methods to extend SRL's `XPBar <https://villavu.github.io/SRL-Development/xpbar.html>`_ functionality.
*)

{$DEFINE WL_XPBAR_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

//TODO: ADD TO SRL. Fixes issue TRSXPBar.IsOpen() when progress bar is enabled.
function TRSXPBar.IsOpen: Boolean; override;

  function FindBorder: Boolean;
  const
    COLOR_BORDER = $233038;
  var C: Int32;
  begin
    C := SRL.CountColor(COLOR_BORDER, Self.Bounds);
    case Self.Width of   // With / without progress bar
{Smallest} 119: Result := (C = 292) or (C = 175);
{Medium}   129: Result := (C = 312) or (C = 185);
{Large}    140: Result := (C = 334) or (C = 196);
    end;
  end;

  function UpdateLocation: Boolean;
  var
    Location: ERSXPBarLocation;
    Size: ERSXPBarSize;
  begin
    for Location in ERSXPBarLocation do
      for Size in ERSXPBarSize do
      begin
        Self.Cache := [];
        Self.Alignment := Self.Alignments[Location][Size];
        if FindBorder() then
        begin
          case Size of
            ERSXPBarSize.SMALL:  Self.Font := RS_FONT_PLAIN_11;
            ERSXPBarSize.MEDIUM: Self.Font := RS_FONT_PLAIN_12;
            ERSXPBarSize.LARGE:  Self.Font := RS_FONT_BOLD_12;
          end;

          Exit(True);
        end;
      end;
  end;

begin
  Result := FindBorder() or UpdateLocation();
end;

function TRSXPBar.EarnedXP(CurrentXP: Int32): Boolean;
begin
  Result := CurrentXP > WL.PreviousXP;

  if Result then
  begin
    WL.Activity.Restart;
    WL.PreviousXP := CurrentXP;
  end;
end;

function TRSXPBar.EarnedXP(): Boolean; overload;
begin
  Result := Self.EarnedXP(Self.Read);
end;

function TRSXPBar.WaitXP(WaitTime: Int32 = 600; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 200);

  Result := WaitUntil(Self.EarnedXP, Interval, WaitTime);
end;


function TRSXPBar.CircleIsOpen(): Boolean;
var
  TPA: TPointArray;
begin
  if FindColors(TPA, 11592943, Minimap.GetXPCircle.Bounds) then
    Result := Length(Minimap.GetXPCircle().Filter(TPA)) > 50;
end;

function TRSXPBar.Read(): Int32; override;
begin
  Result := -1;

  if not Self.CircleIsOpen() then
    Self.Open();

  if Self.IsOpen() then
    Result := OCR.RecognizeNumber(Self.Bounds, TOCRColorRule.Create([$FFFFFF]), Self.Font);

  WL.XPBarIsSetup := (Result >= WL.PreviousXP);

  if WL.XPBarIsSetup then
    Exit;

  //TODO: FIX XPBARSETUP
  //if not MainScreen.HasInterface then
  //  XPBarSetup.SetXPBarProperly;
end;


