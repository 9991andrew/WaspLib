(*
MainScreen
====
Methods to interact with the MainScreen.
*)

{$DEFINE WL_MAINSCREEN_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
ObjectFinder.Unlimit
~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSObjectFinder.Unlimit;

Resets ObjectFinder size limits
*)
procedure TRSObjectFinder.Unlimit;
begin
  Self.MinLongSide := 0;
  Self.MaxLongSide := 0;
  Self.MinShortSide := 0;
  Self.MaxShortSide := 0;
end;

(*
ObjectFinder.Unlimited
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSObjectFinder.Unlimited: TRSObjectFinder;

Returns a copy of the ObjectFinder with the size limits removed.
*)
function TRSObjectFinder.Unlimited: TRSObjectFinder;
begin
  Result := Self;
  Result.MinShortSide := 0;
  Result.MaxShortSide := 0;
  Result.MinLongSide := 0;
  Result.MaxLongSide := 0;
end;


(*
MainScreen.IsVisible
~~~~~~~~~~~~~~~~~~~~
.. pascal::
  function TRSMainScreen.IsVisible(P: TPoint): Boolean; override;
  function TRSMainScreen.IsVisible(TPA: TPointArray): Boolean; overload;
  function TRSMainScreen.IsVisible(B: TBox): Boolean; overload;
  function TRSMainScreen.IsVisible(Rect: TRectangle): Boolean; overload;

Checks if the given TPoint, TPointArray, TBox or TRectangle is visible in the MainScreen.
It will return true if it's not behind an interface otherwise returns false.

Example
-------

  Writeln MainScreen.IsVisible(P);
*)
function TRSMainScreen.IsVisible(P: TPoint): Boolean; override;
begin
  Result := Self.Bounds.Contains(P);

  if Result and (RSClient.Mode <> ERSClientMode.FIXED) then
  begin
    if Self.HasInterface then
    begin
      if Bank.Bounds.Contains(P) and Bank.IsOpen then
        Exit(False);
      if Anvil.Bounds.Contains(P) and Anvil.IsOpen then
        Exit(False);
    end;

    if Chat.Bounds.Contains(P) and Chat.IsOpen then
      Exit(False);
    if ChatButtons.Bounds.Contains(P) then
      Exit(False);
    if Minimap.Bounds.Expand(50).Contains(P) then
      Exit(False);
    if GameTabs.Bounds.Contains(P) then
      Exit(False);
  end;
end;

function TRSMainScreen.IsVisible(TPA: TPointArray): Boolean; overload;
begin
  Result := Self.Bounds.Contains(TPA.FilterBox(Self.Bounds));

  if Result and (RSClient.Mode <> ERSClientMode.FIXED) then
  begin
    if Self.HasInterface then
    begin
      if Bank.Bounds.Contains(TPA) and Bank.IsOpen then
        Exit(False);
      if Anvil.Bounds.Contains(TPA) and Anvil.IsOpen then
        Exit(False);
    end;

    if Chat.Bounds.Contains(TPA) and Chat.IsOpen then
      Exit(False);
    if ChatButtons.Bounds.Contains(TPA) then
      Exit(False);
    if Minimap.Bounds.Expand(50).Contains(TPA) then
      Exit(False);
    if GameTabs.Bounds.Contains(TPA) then
      Exit(False);
  end;
end;

function TRSMainScreen.IsVisible(B: TBox): Boolean; overload;
begin
  Result := Self.Bounds.Contains(B.ToRectangle.ToTPA.FilterBox(Self.Bounds));

  if Result and (RSClient.Mode <> ERSClientMode.FIXED) then
  begin
    if Self.HasInterface then
    begin
      if Bank.Bounds.Contains(B) and Bank.IsOpen then
        Exit(False);
      if Anvil.Bounds.Contains(B) and Anvil.IsOpen then
        Exit(False);
    end;

    if Chat.Bounds.Contains(B) and Chat.IsOpen then
      Exit(False);
    if ChatButtons.Bounds.Contains(B) then
      Exit(False);
    if Minimap.Bounds.Expand(50).Contains(B) then
      Exit(False);
    if GameTabs.Bounds.Contains(B) then
      Exit(False);
  end;
end;

function TRSMainScreen.IsVisible(Rect: TRectangle): Boolean; overload;
begin
  Result := Self.Bounds.Contains(Rect.ToTPA.FilterBox(Self.Bounds));

  if Result and (RSClient.Mode <> ERSClientMode.FIXED) then
  begin
    if Self.HasInterface then
    begin
      if Bank.Bounds.Contains(Rect) and Bank.IsOpen then
        Exit(False);
      if Anvil.Bounds.Contains(Rect) and Anvil.IsOpen then
        Exit(False);
    end;

    if Chat.Bounds.Contains(Rect) and Chat.IsOpen then
      Exit(False);
    if ChatButtons.Bounds.Contains(Rect) then
      Exit(False);
    if Minimap.Bounds.Expand(50).Contains(Rect) then
      Exit(False);
    if GameTabs.Bounds.Contains(Rect) then
      Exit(False);
  end;
end;


(*
MainScreen.Filter
~~~~~~~~~~~~~~~~~~~~
.. pascal::
  function TRSMainScreen.Filter(TPA: TPointArray): TPointArray;
  function TRSMainScreen.Filter(BoxArray: TBoxArray): TBoxArray; overload;
  function TRSMainScreen.Filter(RectArray: TRectArray): TRectArray; overload;
  function TRSMainScreen.Filter(PolyArray: TPolyArray): TPolyArray; overload;

Filters the given TPointArray, TBoxArray, TRectArray or TPolyArray and returns only those that are visible in the MainScreen.
*)
function TRSMainScreen.Filter(TPA: TPointArray): TPointArray;
var
  P: TPoint;
begin
  Result := TPA := TPA.FilterBox(Self.Bounds);

  if (Result <> []) and (RSClient.Mode <> ERSClientMode.FIXED) then
  begin
    if Self.HasInterface then
    begin
      TPA := Result.FilterBox(InterfaceArea.Bounds);
      for P in TPA do
        Result.Del(Result.Find(P));
    end;

    if Chat.IsOpen then
    begin
      TPA := Result.FilterBox(Chat.Bounds);
      for P in TPA do
        Result.Del(Result.Find(P));
    end;

    TPA := Result.FilterBox(ChatButtons.Bounds);
    for P in TPA do
      Result.Del(Result.Find(P));

    TPA := Result.FilterBox(Minimap.Bounds.Expand(50));
    for P in TPA do
      Result.Del(Result.Find(P));

    TPA := Result.FilterBox(GameTabs.Bounds);
    for P in TPA do
      Result.Del(Result.Find(P));
  end;
end;

function TRSMainScreen.Filter(BoxArray: TBoxArray): TBoxArray; overload;
var
  B: TBox;
begin
  for B in BoxArray do
    if Self.IsVisible(B) then
      Result += B;
end;

function TRSMainScreen.Filter(RectArray: TRectArray): TRectArray; overload;
var
  Rect: TRectangle;
begin
  for Rect in RectArray do
    if Self.IsVisible(Rect) then
      Result += Rect;
end;

function TRSMainScreen.Filter(PolyArray: TPolyArray): TPolyArray; overload;
var
  Poly: TPoly;
begin
  for Poly in PolyArray do
    Result += Self.Filter(Poly);
end;


(*
MainScreen.GetAdjacentTiles
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.GetAdjacentTiles: TRectArray;

Returns the 4 tiles adjacent to the player in form of a TRectArray.
*)
function TRSMainScreen.GetAdjacentTiles: TRectArray;
var
  Tiles: TPointArray;
  Tile: TPoint;
begin

  Tiles := [
    [Minimap.Center.X + 4, Minimap.Center.Y],
    [Minimap.Center.X - 4, Minimap.Center.Y],
    [Minimap.Center.X, Minimap.Center.Y + 4],
    [Minimap.Center.X, Minimap.Center.Y - 4]
  ];

  for Tile in Tiles do
    Result += Minimap.StaticToMsRect(Tile, 0);
end;

(*
MainScreen.DebugAdjacentTiles
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSMainScreen.DebugAdjacentTiles;

Debugs MainScreen.GetAdjacentTiles.

Example
-------

  MainScreen.DebugAdjacentTiles;
*)
procedure TRSMainScreen.DebugAdjacentTiles;
var
  Bitmap: TMufasaBitmap;
  Rect: TRectangle;
begin
  Bitmap.FromClient;

  for Rect in Self.GetAdjacentTiles do
    Bitmap.DrawRect(Rect, $ffffff);

  ShowBitmap(Bitmap);
  Bitmap.Free;
end;

(*
MainScreen.InMultiCombat
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.InMultiCombat: Boolean;

Returns true if we are in multi combat false if not.

Example
-------

  Writeln MainScreen.InMultiCombat;
*)
function TRSMainScreen.InMultiCombat: Boolean;
var
  B: TBox;
begin
  case RSClient.Mode of
    ERSClientMode.FIXED:
      begin
        B := InterfaceArea.Bounds;
        B.X1 := B.X2 - 30;
        B.Y1 := B.Y2 - 30;
      end;
    ERSClientMode.RESIZABLE_CLASSIC, ERSClientMode.RESIZABLE_MODERN:
      begin
        B := Minimap.Bounds;
        B.X1 := B.X2 - 30;
        B.Y1 := B.Y2 + 5;
        B.Y2 := B.Y1 + 30;
      end;
  end;

  Result := (SRL.CountColor(CTS0(4902655, 0), B) > 0) and (SRL.CountColor(CTS0(19109, 0), B) > 0);
end;

(*
MainScreen.FindGrave
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.FindGrave: Boolean;

Returns true if we died and there's a grave available somewhere, false otherwise.

Example
-------

  Writeln MainScreen.FindGrave;
*)
function TRSMainScreen.FindGrave: Boolean;
var
  A: TPoint;
  B: TBox;
begin
  case RSClient.Mode of
    ERSClientMode.FIXED:
      begin
        B := InterfaceArea.Bounds;
        B.X1 := B.X2 - 90;
        B.Y1 := B.Y2 - 30;
      end;
    ERSClientMode.RESIZABLE_CLASSIC, ERSClientMode.RESIZABLE_MODERN:
      begin
        A := Minimap.Center;
        B.X1 := A.X - 50;
        B.X2 := A.X + 40;
        B.Y1 := A.Y + 82;
        B.Y2 := A.Y + 120;
      end;
  end;

  Result :=  SRL.CountColor(CTS0(11448498, 0), B) = 51;
end;

(*
MainScreen.LoadingPOH
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.LoadingPOH: Boolean;

Returns true if we are loading a poh.

Example
-------

  Writeln MainScreen.LoadingPOH;
*)
function TRSMainScreen.LoadingPOH: Boolean;
begin
  Result := (SRL.CountColor(CTS0(13127, 0), Self.Bounds) = 892) and
            (SRL.CountColor(CTS0(8247, 0), Self.Bounds) = 900);
end;

(*
MainScreen.WaitLoadingPOH
~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.WaitLoadingPOH: Boolean;

Waits for the POH loading screen returns true if we find a loading screen and successfully wait for it to finish.

Example
-------

  Writeln MainScreen.WaitLoadingPOH;
*)
function TRSMainScreen.WaitLoadingPOH(WaitTime: Int32; Interval: Int32 = -1): Boolean;
var
  t: UInt64;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 1500);

  t := GetTickCount;
  if WaitUntil(Self.LoadingPOH, Interval, WaitTime) then
  begin
    WaitTime := WaitTime - (GetTickCount - t);
    Result := WaitUntil(not Self.LoadingPOH, Interval, WaitTime);
  end;
end;

(*
MainScreen.InCombat
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.InCombat: Boolean;

Returns true if we are currently in combat.
With slow attack speed between you and the enemy this might return false negatives.

Example
-------

  Writeln MainScreen.InCombat;
*)
function TRSMainScreen.InCombat: Boolean;
begin
  Result := XPDrop.FindDrop or (Self.FindHitsplats <> []);
end;

(*
MainScreen.WaitInCombat
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.WaitInCombat(WaitTime: Int32; Interval: Int32 = -1): Boolean;

Waits the specified **WaitTime** until we are in combat.

Example
-------

  MainScreen.WaitInCombat(5000);
*)
function TRSMainScreen.WaitInCombat(WaitTime: Int32; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 1500);

  Result := WaitUntil(Self.InCombat, Interval, WaitTime);
end;

(*
MainScreen.FindArrow
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.FindArrow(out TPA: TPointArray): Boolean;
.. pascal:: function TRSMainScreen.FindArrow: Boolean; overload;

Returns true if there's a yellow arrow on screen.
If a TPointArray is passed as a parameter it will return with the location of the arrow tip.

Example
-------

  Writeln MainScreen.FindArrow;
*)
function TRSMainScreen.FindArrow(out TPA: TPointArray): Boolean;
var
  TempTPA: TPointArray;
begin
  Result := (SRL.FindColors(TempTPA, CTS0(2291706, 0), Self.Bounds) > 0) and
            (Length(TempTPA) <= 35);
  if Result then
    TPA := Self.Filter(TempTPA);
end;

function TRSMainScreen.FindArrow: Boolean; overload;
var
  TPA: TPointArray;
begin
  Result := (SRL.FindColors(TPA, CTS0(2291706, 0), Self.Bounds) > 0) and
            (Length(TPA) <= 35);
end;

(*
MainScreen.WaitArrow
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSMainScreen.WaitArrow(TPA: TPointArray; WaitTime: Int32 = 600; Interval: Int32 = -1): Boolean;
.. pascal:: function TRSMainScreen.WaitArrow(WaitTime: Int32 = 600; Interval: Int32 = -1): Boolean;

Waits **WaitTime** for a yellow arrow to appear on MainScreen.
If a TPointArray is passed as a parameter it will return with the location of the arrow tip.

Example
-------

  Writeln MainScreen.WaitArrow;
*)
function TRSMainScreen.WaitArrow(out TPA: TPointArray; WaitTime: Int32 = 120; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 200);

  Result := WaitUntil(Self.FindArrow(TPA), Interval, WaitTime);
end;

function TRSMainScreen.WaitArrow(WaitTime: Int32 = 120; Interval: Int32 = -1): Boolean; overload;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 200);

  Result := WaitUntil(Self.FindArrow, Interval, WaitTime);
end;

