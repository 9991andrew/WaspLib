{$DEFINE WL_RSCLIENT_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

{$IFNDEF SRL_DISABLE_REMOTEINPUT}

function EIOS_IsInputEnabled(eios: Pointer): Boolean;
begin
  Result := EIOS_IsMouseInputEnabled(eios) and EIOS_IsKeyboardInputEnabled(eios);
end;

procedure EIOS_SetInputEnabled(eios: Pointer; enabled: Boolean);
begin
  EIOS_SetMouseInputEnabled(eios, enabled);
  EIOS_SetKeyboardInputEnabled(eios, enabled);
end;

procedure TRemoteInput.DisableRealInput();
begin
  if EIOS_IsInputEnabled(Self.EIOS) then
    EIOS_SetInputEnabled(Self.EIOS, False);
end;

procedure TRemoteInput.EnableRealInput();
begin
  if not EIOS_IsInputEnabled(Self.EIOS) then
    EIOS_SetInputEnabled(Self.EIOS, True);
end;

procedure TRemoteInput.Setup(); override;
begin
  inherited;

  if WLSettings.RemoteInput.BlockInput then
  begin
    Self.DisableRealInput();

    AddOnTerminate(@Self.EnableRealInput);
    AddOnPause(@Self.EnableRealInput);
    AddOnResume(@Self.DisableRealInput);
  end
  else
    Self.EnableRealInput();
end;

{$ENDIF}


procedure TRSClient.FixFocusedClient();
const
  WARNING = 'You don''t seem to be using the official osrs client.'+ LineEnding +
            'Only the official osrs client is supported.' + LineEnding + LineEnding +
            'Please don''t make bug reports if you are using any other client.';
var
  path: String;
  child: TWindowHandle;
  children: TWindowHandleArray;
begin
  path := GetProcessPath(GetSimbaTargetPID()).ToLower();

  if not path.ContainsAny(['jagex', 'java', '.jar']) then
    ShowDialog('Warning!', WARNING, mtWarning, [mbOK]);

  child := GetSimbaTargetWindow();
  children := child.GetChildren(True);

  while children <> [] do
  begin
    child    := children[0];
    children := child.GetChildren(True);
  end;

  SetTargetWindow(child);
end;

procedure TRSClient.Setup(); override;
begin
  Self.Name := 'RSClient';

  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
  {$IFNDEF SCRIPT_GUI}
    Self.RemoteInput.Setup();
  {$ENDIF}
  {$ENDIF}
end;
