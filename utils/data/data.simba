{$DEFINE WL_DATA_INCLUDED}
{$IFNDEF WL_UTILS}
  {$I WaspLib/utils.simba}
{$ENDIF}

//ItemData
const
  MONSTER_NAMES_FILE  = {$macro CURRENT_DIRECTORY} + '/monster-names';
  MONSTERS_JSON  = {$macro CURRENT_DIRECTORY} + '/monsters.json';

type
  TRSItemData = record(TSRLBaseRecord)
    ItemNames: TStringList;
    WikiData: TJSONObject;
    OSRSBoxData: TJSONObject;
    ReloadTimer: TCountDown;
    WikiPriceIsSetup: Boolean;
    OSRSBoxIsSetup: Boolean;
  end;

  ERSMonsterAttackType = (
    MELEE_ATTACK, RANGED_ATTACK, MAGIC_ATTACK, TYPELESS_ATTACK
  );

  TRSMonsterDrop = record
    Item: String;
    ID: String;
    Stackable: Boolean;
    Noted: Boolean;
    Quantity: Int32;
  end;

  TRSMonsterData = record(TSRLBaseRecord)
    MonsterNames: TStringList;
    CurrentMonster: String;
    Data: TJSONObject;
    Size: Int32;
    Hitpoints: Int32;
    MaxHit: Int32;
    AttackTypes: array of ERSMonsterAttackType;
    Drops: array of TRSMonsterDrop;
    IsSetup: Boolean;
  end;

procedure TRSItemData.Free;
begin
  ItemNames.Free;
  WikiData.Free;
end;


procedure TRSItemData.SetupWikiPrices;
begin
  if not WikiPriceIsSetup then
  begin
    Name := 'ItemData';
    ItemNames.Init;
    ItemNames.LoadFromFile(ITEM_FINDER_ITEM_NAMES_FILE);
    ItemNames.SetSorted(True);

    AddOnTerminate(@Self.Free);
    WikiPriceIsSetup := True;
    ReloadTimer.Setup(1200000);
  end;

  DebugLn('Loading item prices from the wiki API.');
  WikiData.Init(GetPage('https://prices.runescape.wiki/api/v1/osrs/latest'));
  ReloadTimer.Restart;
end;

function TRSItemData.GetIDs(Item: String): TStringArray;
type
  TStringList = TStringList;

  function TStringList.NameFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).Before('=');
  end;

  function TStringList.ValueFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).After('=');
  end;

var
  ItemID: Int32;
begin
  if Item.IsDigit then
    Result := Result + Item
  else
  begin
    ItemID := Self.ItemNames.IndexOfName(Item);
    while SameText(Item, Self.ItemNames.NameFromIndex(ItemID)) do
    begin
      Result := Result + Self.ItemNames.ValueFromIndex(ItemID);

      Inc(ItemID);
    end;
  end;
end;

function TRSItemData.GetData(Item: String): TJSONObject;
begin
  if not WikiPriceIsSetup then
    Self.SetupWikiPrices;

  if Item = '' then
    Exit;

  DisableDebugging := True;
  DebugLn('Getting ' + Item + ' data from OSRSBox API.');
  OSRSBoxData.Init(GetPage('https://www.osrsbox.com/osrsbox-db/items-json/' + Item + '.json'));
  Result := OSRSBoxData;

  if not OSRSBoxIsSetup then
  begin
    AddOnTerminate(@OSRSBoxData.Free);
    OSRSBoxIsSetup := True;
  end;
end;

function TRSItemData.GetTradeableID(Item: TRSItem): String;
var
  IDs: TStringArray;
  ID: String;
begin
  if not WikiPriceIsSetup then
    Self.SetupWikiPrices;

  if VarToStr(Item) = '' then
    Exit;

  IDs := Self.GetIDs(Item);
  for ID in IDs do
  begin
    if Self.GetData(ID).getBoolean('tradeable_on_ge') then
      Exit(ID);
  end;
end;

function TRSItemData.GetPrice(Item: TRSItem): TJSONObject;
var
  ID: String;
begin
  if ReloadTimer.IsPaused or ReloadTimer.IsFinished then
    Self.SetupWikiPrices;

  ID := Self.GetTradeableID(Item);

  if ID <> '' then
    Result := WikiData.getJSONObject('data').getJSONObject(ID);
end;


function TRSItemData.GetWikiInt(Item: TRSItem; Key: String): Int32;
var
  JSONObject: TJSONObject := Self.GetPrice(Item);
begin
  if JSONObject <> [] then
    Result := JSONObject.getInt(Key);
end;

function TRSItemData.GetAverage(Item: TRSItem): Int32;
var
  ItemStr: String := VarToStr(Item).Lower;
  Low, High: Int32;
begin
  if ItemStr.Contains(
    ['coins', '995', '996', '997',
     '998', '999', '1000', '1001',
     '1002', '1003', '1004']) then
    Exit(1);

  Low := Self.GetWikiInt(Item, 'low');
  High := Self.GetWikiInt(Item, 'high');

  if (Low = 0) and (High = 0) then
    Exit;

  if High = 0 then
    Result := Low
  else if Low = 0 then
    Result := High
  else
    Result := Round((High + Low) / 2);
end;


function TRSItemData.GetOSRSBoxInt(Item: TRSItem; Key: String): Int32;
var
  Str: String;
begin
  Str := Self.GetTradeableID(Item);
  if Str = '' then
    Exit;

  Str := Self.GetData(Str).getString(Key);

  if Str.Contains('null') then
    Result := 0
  else
    Result := StrToInt(Str);
end;

function TRSItemData.GetOSRSBoxBoolean(Item: TRSItem; Key: String): Boolean;
var
  Str: String;
begin
  Str := Self.GetTradeableID(Item);
  if Str = '' then
    Exit;

  Result := Self.GetData(Str).getBoolean(Key);
end;


function TRSItemData.GetHighAlch(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'highalch') - Self.GetAverage('561');
end;

function TRSItemData.GetHighAlchProfit(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'highalch') -
              Self.GetAverage(Item) - Self.GetAverage('561');
end;

function TRSItemData.GetLowAlchProfit(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'lowalch') -
              Self.GetAverage(Item) - Self.GetAverage('561');
end;

var
  ItemData: TRSItemData;


//MonsterData
function TRSMonsterData.GetIDs(Monster: String): TStringArray;
type
  TStringList = TStringList;

  function TStringList.NameFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).Before('=');
  end;

  function TStringList.ValueFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).After('=');
  end;

var
  MonsterID: Int32;
begin
  if Monster.IsDigit then
    Result := Result + Monster
  else
  begin
    MonsterID := Self.MonsterNames.IndexOfName(Monster);
    while SameText(Monster, Self.MonsterNames.NameFromIndex(MonsterID)) do
    begin
      Result := Result + Self.MonsterNames.ValueFromIndex(MonsterID);

      Inc(MonsterID);
    end;
  end;
end;

function TRSMonsterData.GetID(Monster: String): String;
var
  IDs: TStringArray := Self.GetIDs(Monster);
begin
  if IDs <> [] then
    Result := IDs[0];
end;

procedure TRSMonsterData.Free;
begin
  MonsterNames.Free;
  Data.Free;
end;

procedure TRSMonsterData.Setup;
begin
  if not IsSetup then
  begin
    Name := 'MonsterData';

    MonsterNames.Init;
    MonsterNames.LoadFromFile(MONSTER_NAMES_FILE);
    MonsterNames.SetSorted(True);

    AddOnTerminate(@Data.Free);
    IsSetup := True;
  end;

  DebugLn('Loading monsters.json.');

  Data.Init(ReadFileContents(MONSTERS_JSON));
end;


function TRSMonsterData.GetInt(Monster, Key: String): Int32;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getInt(Key);
end;

function TRSMonsterData.GetDouble(Monster, Key: String): Double;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getDouble(Key);
end;

function TRSMonsterData.GetString(Monster, Key: String): String;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getString(Key);
end;

function TRSMonsterData.GetBoolean(Monster, Key: String): Boolean;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getBoolean(Key);
end;

function TRSMonsterData.GetJSONArray(Monster, Key: String): TJSONArray;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getJSONArray(Key);
end;

function TRSMonsterData.GetJSONObject(Monster, Key: String): TJSONObject;
begin
  if not IsSetup then
    Self.Setup;

  Result := Data.getJSONObject(Self.GetID(Monster)).getJSONObject(Key);
end;

function TRSMonsterData.GetAttackTypes(Monster: String): array of ERSMonsterAttackType;
var
  JSONArray: TJSONArray;
  i: Int32;
begin
  if not IsSetup then
    Self.Setup;

  JSONArray := Self.GetJSONArray(Monster, 'attack_type');

  for i := 0 to JSONArray.length - 1 do
    case JSONArray.getString(i) of
      'stab', 'slash', 'crush':
        Result += ERSMonsterAttackType.MELEE_ATTACK;
      'ranged':
        Result += ERSMonsterAttackType.RANGED_ATTACK;
      'magic':
        Result += ERSMonsterAttackType.MAGIC_ATTACK;
      'typeless':
        Result += ERSMonsterAttackType.TYPELESS_ATTACK;
    end;
end;

function TRSMonsterData.GetDrop(DropJSON: TJSONObject): TRSMonsterDrop;
var
  Str: String;
begin
  Result.ID := DropJSON.getString('id');
  Result.Item := DropJSON.getString('name');
  Str := DropJSON.getString('quantity');
  if Str.Contains('-') then
  begin
    Result.Quantity := Round(StrToInt(Str.Before('-')) + StrToInt(Str.After('-')) / 2);
  end
  else
    Result.Quantity := StrToInt(Str);
  Result.Noted := DropJSON.getBoolean('noted');
  Result.Stackable := Result.Noted;
end;

function TRSMonsterData.GetDrops(Monster: String): array of TRSMonsterDrop;
var
  JSONArray: TJSONArray;
  i: Int32;
  Drop: TRSMonsterDrop;
begin
  if not IsSetup then
    Self.Setup;

  DebugLn('Loading ' + Monster + ' drops, this might take up to a 1 minute.');

  JSONArray := Self.GetJSONArray(Monster, 'drops');

  for i := 0 to JSONArray.length - 1 do
  begin
    Drop := [];
    Drop := Self.GetDrop(JSONArray.getJSONObject(i));

    if not Drop.Stackable then
    begin
      if Drop.ID.Contains(['995', '996', '997', '998', '999', '1000', '1001',
                           '1002', '1003', '1004']) then
        Drop.Stackable := True
      else
        Drop.Stackable := ItemData.GetOSRSBoxBoolean(Drop.ID, 'stackable');
    end;

    Result += Drop;
  end;
end;

var
  MonsterData: TRSMonsterData;


//WeaponData
type
  TRSWeaponData = record(TSRLBaseRecord)
    WikiSpecAttacks: String;

    IsSetup: Boolean;
  end;

procedure TRSWeaponData.Setup;
begin
  if IsSetup then
    Exit;
  Name := 'WeaponData';
  WikiSpecAttacks := GetPage('https://oldschool.runescape.wiki/w/Special_attacks').Lower.After('mw-headline');
end;

function TRSWeaponData.GetSpec(Weapon: TRSItem): Int32;
var
  WeaponStr: String;
  Str: String;
begin
  if not IsSetup then
    Self.Setup;

  WeaponStr := ToStr(Weapon);
  DebugLn('Getting ' + WeaponStr + ' spec data from the wiki.');

  Str :=  WikiSpecAttacks.After(WeaponStr.Lower);

  if Str = WikiSpecAttacks then
    Exit(200);

  Result := StrToInt(Str.After('energy used').After('<td>').Before('%'));

  DebugLn(WeaponStr + ' spec setup with ' + ToStr(Result) + '% energy.');
end;

var
  WeaponData: TRSWeaponData;



begin
  ItemData.SetupWikiPrices;
  MonsterData.Setup;
end;

