{$DEFINE WL_DATA_INCLUDED}
{$IFNDEF WL_UTILS}
  {$I WaspLib/utils.simba}
{$ENDIF}

//ItemData
const
  MONSTER_NAMES_FILE  = {$macro CURRENT_DIRECTORY} + '/monster-names';
  MONSTERS_JSON  = {$macro CURRENT_DIRECTORY} + '/monsters.json';
  OSRSBOX_URL = 'https://raw.githubusercontent.com/Flipping-Utilities/osrsbox-db/main/docs/items-json/';

type
  TRSItemData = record
    ItemNames: TStringList;
    WikiData: TJSONObject;
    OSRSBoxData: TJSONObject;
    ReloadTimer: TCountDown;
    WikiPriceIsSetup: Boolean;
    OSRSBoxIsSetup: Boolean;
    WikiDisconnected: Boolean;
    OSRSBoxDisconnected: Boolean;
  end;

  ERSMonsterAttackType = (
    MELEE_ATTACK, RANGED_ATTACK, MAGIC_ATTACK, TYPELESS_ATTACK
  );

  TRSMonsterDrop = record
    Item: String;
    ID: String;
    Stackable: Boolean;
    Noted: Boolean;
    Quantity: Int32;
  end;

  TRSMonsterData = record
    MonsterNames: TStringList;
    CurrentMonster: String;
    Data: TJSONObject;
    Size: Int32;
    Hitpoints: Int32;
    MaxHit: Int32;
    AttackTypes: array of ERSMonsterAttackType;
    Drops: array of TRSMonsterDrop;
    IsSetup: Boolean;
  end;

procedure TRSItemData.Free;
begin
  Self.ItemNames.Free;
  Self.WikiData.Free;
end;


procedure TRSItemData.SetupWikiPrices;
var
  JSON: String;
begin
  if not Self.WikiPriceIsSetup then
  begin
    Self.ItemNames.Init;
    Self.ItemNames.LoadFromFile(ITEM_FINDER_ITEM_NAMES_FILE);
    Self.ItemNames.SetSorted(True);

    AddOnTerminate(@Self.Free);
    Self.WikiPriceIsSetup := True;
    Self.ReloadTimer.Setup(1200000);
  end;

  WriteLn 'Loading item prices from the wiki API.';
  JSON := TryGetPage('https://prices.runescape.wiki/api/v1/osrs/latest');

  if JSON = '' then
  begin
    WriteLn 'We don''t seem to be able to connect to https://prices.runescape.wiki';
    Self.WikiDisconnected := True;
    Exit;
  end;

  Self.WikiData.Init(JSON);
  Self.ReloadTimer.Restart;
end;

function TRSItemData.GetIDs(Item: String): TStringArray;
type
  TStringList = TStringList;

  function TStringList.NameFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).Before('=');
  end;

  function TStringList.ValueFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).After('=');
  end;

var
  ItemID: Int32;
begin
  if Item.IsDigit then
    Result := Result + Item
  else
  begin
    ItemID := Self.ItemNames.IndexOfName(Item);
    while SameText(Item, Self.ItemNames.NameFromIndex(ItemID)) do
    begin
      Result := Result + Self.ItemNames.ValueFromIndex(ItemID);

      Inc(ItemID);
    end;
  end;
end;

function TRSItemData.GetData(Item: String): TJSONObject;
var
  JSON: String;
begin
  if Self.WikiDisconnected or Self.OSRSBoxDisconnected then
    Exit;

  if not Self.WikiPriceIsSetup then
    Self.SetupWikiPrices;

  if Item = '' then
    Exit;

  JSON := TryGetPage(OSRSBOX_URL + Item + '.json');
  if JSON = '' then
  begin
    WriteLn 'We don''t seem to be able to connect to https://www.osrsbox.com';
    Self.OSRSBoxDisconnected := True;
    Exit;
  end;

  Self.OSRSBoxData.Init(Json);
  Result := Self.OSRSBoxData;

  if not Self.OSRSBoxIsSetup then
  begin
    AddOnTerminate(@Self.OSRSBoxData.Free);
    Self.OSRSBoxIsSetup := True;
  end;
end;

function TRSItemData.GetTradeableID(Item: TRSItem): String;
var
  IDs: TStringArray;
  ID: String;
  OSRSBoxJSON: TJSONObject;
begin
  if not Self.WikiPriceIsSetup then
    Self.SetupWikiPrices;

  if ToStr(Item) = '' then
    Exit;

  IDs := Self.GetIDs(Item);
  for ID in IDs do
  begin
    OSRSBoxJSON := Self.GetData(ID);
    if (OSRSBoxJSON <> nil) and Self.GetData(ID).getBoolean('tradeable_on_ge') then
      Exit(ID);
  end;
end;

function TRSItemData.GetPrice(Item: TRSItem): TJSONObject;
var
  ID: String;
  WikiJSON: TJSONObject;
begin
  if Self.ReloadTimer.IsPaused or Self.ReloadTimer.IsFinished then
    Self.SetupWikiPrices;

  ID := Self.GetTradeableID(Item);

  if ID <> '' then
  begin
    WikiJSON := Self.WikiData.getJSONObject('data');
    if WikiJSON <> nil then
      Result := WikiJSON.getJSONObject(ID);
  end;
end;


function TRSItemData.GetWikiInt(Item: TRSItem; Key: String): Int32;
var
  JSONObject: TJSONObject := Self.GetPrice(Item);
begin
  if JSONObject <> nil then
    Result := JSONObject.getInt(Key);
end;

function TRSItemData.GetAverage(Item: TRSItem): Int32;
var
  ItemStr: String := ToStr(Item).Lower;
  Low, High: Int32;
begin
  if ItemStr.Contains(
    ['coins', '995', '996', '997',
     '998', '999', '1000', '1001',
     '1002', '1003', '1004']) then
    Exit(1);

  Low := Self.GetWikiInt(Item, 'low');
  High := Self.GetWikiInt(Item, 'high');

  if (Low = 0) and (High = 0) then
    Exit;

  if High = 0 then
    Result := Low
  else if Low = 0 then
    Result := High
  else
    Result := Round((High + Low) / 2);
end;


function TRSItemData.GetOSRSBoxInt(Item: TRSItem; Key: String): Int32;
var
  Str: String;
  OSRSBoxJSON: TJSONObject;
begin
  Str := Self.GetTradeableID(Item);
  if Str = '' then
    Exit;

  OSRSBoxJSON := Self.GetData(Str);
  if OSRSBoxJSON = nil then
    Exit;

  Str := OSRSBoxJSON.getString(Key);

  if Str.Contains('null') then
    Result := 0
  else
    Result := StrToInt(Str);
end;

function TRSItemData.GetOSRSBoxBoolean(Item: TRSItem; Key: String): Boolean;
var
  Str: String;
  OSRSBoxJSON: TJSONObject;
begin
  Str := Self.GetTradeableID(Item);
  if Str = '' then
    Exit;

  OSRSBoxJSON := Self.GetData(Str);
  if OSRSBoxJSON = nil then
    Exit;

  Result := OSRSBoxJSON.getBoolean(Key);
end;


function TRSItemData.GetHighAlch(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'highalch') - Self.GetAverage('561');
end;

function TRSItemData.GetHighAlchProfit(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'highalch') -
              Self.GetAverage(Item) - Self.GetAverage('561');
end;

function TRSItemData.GetLowAlchProfit(Item: TRSItem): Int32;
var
  Str: String := Self.GetTradeableID(Item);
begin
  if Str <> '' then
    Result := Self.GetOSRSBoxInt(Str, 'lowalch') -
              Self.GetAverage(Item) - Self.GetAverage('561');
end;

var
  ItemData: TRSItemData;


//MonsterData
function TRSMonsterData.GetIDs(Monster: String): TStringArray;
type
  TStringList = TStringList;

  function TStringList.NameFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).Before('=');
  end;

  function TStringList.ValueFromIndex(Index: Int32): String; override;
  begin
    if (Index >= 0) and (Index < Self.GetCount()) then
      Result := Self.GetStrings(Index).After('=');
  end;

var
  MonsterID: Int32;
begin
  if Monster.IsDigit then
    Result := Result + Monster
  else
  begin
    MonsterID := Self.MonsterNames.IndexOfName(Monster);
    while SameText(Monster, Self.MonsterNames.NameFromIndex(MonsterID)) do
    begin
      Result := Result + Self.MonsterNames.ValueFromIndex(MonsterID);

      Inc(MonsterID);
    end;
  end;
end;

function TRSMonsterData.GetID(Monster: String): String;
var
  IDs: TStringArray := Self.GetIDs(Monster);
begin
  if IDs <> [] then
    Result := IDs[0];
end;

procedure TRSMonsterData.Free;
begin
  Self.MonsterNames.Free;
  Self.Data.Free;
end;

procedure TRSMonsterData.Setup;
begin
  if not Self.IsSetup then
  begin
    Self.MonsterNames.Init;
    Self.MonsterNames.LoadFromFile(MONSTER_NAMES_FILE);
    Self.MonsterNames.SetSorted(True);

    AddOnTerminate(@Self.Data.Free);
    Self.IsSetup := True;
  end;

  WriteLn('Loading monsters.json.');

  Self.Data.Init(ReadFileContents(MONSTERS_JSON));
end;


function TRSMonsterData.GetInt(Monster, Key: String): Int32;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getInt(Key);
end;

function TRSMonsterData.GetDouble(Monster, Key: String): Double;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getDouble(Key);
end;

function TRSMonsterData.GetString(Monster, Key: String): String;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getString(Key);
end;

function TRSMonsterData.GetBoolean(Monster, Key: String): Boolean;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getBoolean(Key);
end;

function TRSMonsterData.GetJSONArray(Monster, Key: String): TJSONArray;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getJSONArray(Key);
end;

function TRSMonsterData.GetJSONObject(Monster, Key: String): TJSONObject;
begin
  if not Self.IsSetup then
    Self.Setup;

  Result := Self.Data.getJSONObject(Self.GetID(Monster)).getJSONObject(Key);
end;

function TRSMonsterData.GetAttackTypes(Monster: String): array of ERSMonsterAttackType;
var
  JSONArray: TJSONArray;
  i: Int32;
begin
  if not Self.IsSetup then
    Self.Setup;

  JSONArray := Self.GetJSONArray(Monster, 'attack_type');

  for i := 0 to JSONArray.length - 1 do
    case JSONArray.getString(i) of
      'stab', 'slash', 'crush':
        Result += ERSMonsterAttackType.MELEE_ATTACK;
      'ranged':
        Result += ERSMonsterAttackType.RANGED_ATTACK;
      'magic':
        Result += ERSMonsterAttackType.MAGIC_ATTACK;
      'typeless':
        Result += ERSMonsterAttackType.TYPELESS_ATTACK;
    end;
end;

function TRSMonsterData.GetDrop(DropJSON: TJSONObject): TRSMonsterDrop;
var
  Str: String;
begin
  Result.ID := DropJSON.getString('id');
  Result.Item := DropJSON.getString('name');
  Str := DropJSON.getString('quantity');
  if Str.Contains('-') then
  begin
    Result.Quantity := Round(StrToInt(Str.Before('-')) + StrToInt(Str.After('-')) / 2);
  end
  else
    Result.Quantity := StrToInt(Str);
  Result.Noted := DropJSON.getBoolean('noted');
  Result.Stackable := Result.Noted;
end;

function TRSMonsterData.GetDrops(Monster: String): array of TRSMonsterDrop;
var
  JSONArray: TJSONArray;
  i: Int32;
  Drop: TRSMonsterDrop;
begin
  if not Self.IsSetup then
    Self.Setup;

  WriteLn('Loading ' + Monster + ' drops, this might take up to a 1 minute.');

  JSONArray := Self.GetJSONArray(Monster, 'drops');

  for i := 0 to JSONArray.length - 1 do
  begin
    Drop := [];
    Drop := Self.GetDrop(JSONArray.getJSONObject(i));

    if not Drop.Stackable then
    begin
      if Drop.ID.Contains(['995', '996', '997', '998', '999', '1000', '1001',
                           '1002', '1003', '1004']) then
        Drop.Stackable := True
      else
        Drop.Stackable := ItemData.GetOSRSBoxBoolean(Drop.ID, 'stackable');
    end;

    Result += Drop;
  end;
end;

var
  MonsterData: TRSMonsterData;


//WeaponData
type
  TRSWeaponData = record(TSRLBaseRecord)
    WikiSpecAttacks: String;

    IsSetup: Boolean;
  end;

procedure TRSWeaponData.Setup;
begin
  if Self.IsSetup then
    Exit;
  Self.Name :='WeaponData';
  Self.WikiSpecAttacks := GetPage('https://oldschool.runescape.wiki/w/Special_attacks').Lower.After('mw-headline');
end;

function TRSWeaponData.GetSpec(Weapon: TRSItem): Int32;
var
  WeaponStr: String;
  Str: String;
begin
  if not Self.IsSetup then
    Self.Setup;

  WeaponStr := ToStr(Weapon);
  Self.DebugLn('Getting ' + WeaponStr + ' spec data from the wiki.');

  Str :=  Self.WikiSpecAttacks.After(WeaponStr.Lower);

  if Str = Self.WikiSpecAttacks then
    Exit(200);

  Result := StrToInt(Str.After('energy used').After('<td>').Before('%'));

  Self.DebugLn(WeaponStr + ' spec setup with ' + ToStr(Result) + '% energy.');
end;

var
  WeaponData: TRSWeaponData;



begin
  ItemData.SetupWikiPrices;
  MonsterData.Setup;
end;

