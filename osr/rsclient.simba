{$DEFINE WL_RSCLIENT_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

{$IFNDEF SRL_DISABLE_REMOTEINPUT}

function EIOS_IsInputEnabled(eios: Pointer): Boolean;
begin
  Result := EIOS_IsMouseInputEnabled(eios) and EIOS_IsKeyboardInputEnabled(eios);
end;

procedure EIOS_SetInputEnabled(eios: Pointer; enabled: Boolean);
begin
  EIOS_SetMouseInputEnabled(eios, enabled);
  EIOS_SetKeyboardInputEnabled(eios, enabled);
end;

procedure TRemoteInput.DisableRealInput();
begin
  if EIOS_IsInputEnabled(Self.EIOS) then
    EIOS_SetInputEnabled(Self.EIOS, False);
end;

procedure TRemoteInput.EnableRealInput();
begin
  if not EIOS_IsInputEnabled(Self.EIOS) then
    EIOS_SetInputEnabled(Self.EIOS, True);
end;

procedure TRemoteInput.Setup(); override;
const
  WARNING = 'You don''t seem to be using the official osrs client.'+ LineEnding +
            'Only the official osrs client is supported.' + LineEnding + LineEnding +
            'Please don''t make bug reports if you are using any other client.';
var
  exePath: String;
begin
  exePath := GetProcessPath(GetSimbaTargetPID()).ToLower();

  if not exePath.ContainsAny(['jagex', 'java', '.jar']) then
    ShowDialog('Warning!', WARNING, mtWarning, [mbOK]);

  inherited;

  if BlockRealInput then
  begin
    Self.DisableRealInput();

    AddOnTerminate(@Self.EnableRealInput);
    AddOnPause(@Self.EnableRealInput);
    AddOnResume(@Self.DisableRealInput);
  end
  else
    Self.EnableRealInput();
end;

{$ENDIF}

procedure TRSClient.Setup(); override;
begin
  Self.Name :='RSClient';

  {$IFNDEF SRL_DISABLE_REMOTEINPUT}
    {$IFNDEF SCRIPT_GUI}
      Self.RemoteInput.Setup();
    {$ENDIF}
  {$ENDIF}

  Self.Mode := Self.DetectClientMode();
  if Self.Mode = ERSClientMode.UNKNOWN then
    Self.Mode := ERSClientMode.FIXED; // Fixed as default so we can login if needed

  Self.ClientModeChanged();
end;


