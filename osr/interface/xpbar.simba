(*
XPBar
=====
Methods to extend SRL's `XPBar <https://villavu.github.io/SRL-Development/xpbar.html>`_ functionality.
*)

{$DEFINE WL_XPBAR_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

function TRSXPBar.EarnedXP(CurrentXP: Int32 = 0): Boolean;
begin
  if CurrentXP <= 0 then
    CurrentXP := Self.Read();

  Result := CurrentXP > WL.Cache.XP.Previous;

  if Result then
  begin
    Self.DebugLn('Activity timer reset.');
    WL.Activity.Restart();
    WL.Cache.XP.Previous := CurrentXP;
  end;
end;

function TRSXPBar.WaitXP(WaitTime: Int32 = 600; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := RandomLeft(50, 200);

  Result := WaitUntil(Self.EarnedXP(), Interval, WaitTime);
end;


function TRSXPBar.CircleIsOpen(): Boolean;
var
  TPA: TPointArray;
begin
  if FindColors(TPA, 11592943, Minimap.GetXPCircle.Bounds) then
    Result := Length(Minimap.GetXPCircle().Filter(TPA)) > 50;
end;

function TRSXPBar.Read(): Int32; override;
begin
  Result := -1;

  if not Self.CircleIsOpen() then
    Self.Open();

  Result := inherited;

  WL.Cache.XP.IsSetup := (Result > 0) and (Result >= WL.Cache.XP.Previous);

  if WL.Cache.XP.IsSetup then
  begin
    if WL.Cache.XP.Start = 0 then
      WL.Cache.XP.Start := Result;
    Exit;
  end;

  if not WL.Cache.XP.SetupWasFixed and not MainScreen.HasInterface() then
    XPSetup.Fix();
end;
