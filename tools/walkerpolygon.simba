(*
Walker Polygon Drawer
=====================
Tool to draw polygons in walker maps. This polygons can later be used to detect presence inside or outside of said polygoon of the player, minimap dot, or anything really.
*)

{$DEFINE SRL_DISABLE_REMOTEINPUT}
{$I SRL/osr.simba}
{$I WaspLib/osr.simba}

const
  MAP_NAME: String = 'world';

type
  TPolyDrawerForm = record
    Form: TForm;
    ImageBox: TSimbaImageBox;

    SelectedPolygon: Int32;
    NodeArray: T2DPointArray;

    PolyIndexEdit: TEdit;
    CoordinateEdit: TEdit;
    SelectedNode: Int32;
    Dragging: Boolean;
    Map: String;
    Name: String;

    Offset: TPoint;
  end;

var
  PolyDrawer: TPolyDrawerForm;

begin
  // Change the map here
  PolyDrawer.Map := TRSWalker.FindMap(MAP_NAME);

  if 'mainland' in MAP_NAME then
    PolyDrawer.Offset := [3583, 0]
  else if ('x' in MAP_NAME) and ('y' in MAP_NAME) then
  begin
    PolyDrawer.Offset.X := StrToInt(MAP_NAME.After('x').Before('y'));
    PolyDrawer.Offset.Y := StrToInt(MAP_NAME.After('y'));
  end
  else
    PolyDrawer.Offset := [0, 0];
end;

procedure TPolyDrawerForm.OnMouseMove(Sender: TObject; Shift: TShiftState; X, Y: Int32);
begin
  if Dragging and (SelectedNode > -1) then
  begin
    NodeArray[SelectedPolygon][SelectedNode].X := X;
    NodeArray[SelectedPolygon][SelectedNode].Y := Y;
    NodeArray[SelectedPolygon][SelectedNode] := NodeArray[SelectedPolygon][SelectedNode] + Offset;
    PolyIndexEdit.setCaption(ToStr(SelectedPolygon));
    CoordinateEdit.setCaption(ToStr(NodeArray[SelectedPolygon][SelectedNode].X) + ', ' + ToStr(NodeArray[SelectedPolygon][SelectedNode].Y));
    ImageBox.Update;
  end;
end;


procedure TPolyDrawerForm.AddToArray;
begin
  SelectedPolygon := Length(NodeArray);
  SetLength(NodeArray, SelectedPolygon + 1);
end;

procedure TPolyDrawerForm.RemoveFromArray;
begin
  Delete(NodeArray, SelectedPolygon, 1);
  SelectedPolygon := High(NodeArray);
end;


procedure TPolyDrawerForm.AddNode(p: TPoint);
var
  n, c: Int32;
begin
  if SelectedPolygon < 0 then
    SelectedPolygon := 0;

  n := Length(NodeArray);
  if (n = 0) or (SelectedPolygon > n) then
    SetLength(NodeArray, n + 1);

  c := Length(NodeArray[SelectedPolygon]);
  SetLength(NodeArray[SelectedPolygon], c + 1);

  NodeArray[SelectedPolygon][c] := p + Offset;

  SelectedNode := c;
  ImageBox.Update;
  PolyIndexEdit.setCaption(ToStr(SelectedPolygon));
  CoordinateEdit.setCaption(ToStr(NodeArray[SelectedPolygon][SelectedNode].X) + ', ' + ToStr(NodeArray[SelectedPolygon][SelectedNode].Y));
end;

procedure TPolyDrawerForm.DeleteNode(n: Int32);
var
  i,curr: Int32;
  marked: TIntegerArray;
  p: TPoint;
begin
  p := NodeArray[SelectedPolygon][n];
  marked += n;

  repeat
    curr := marked.Pop;

    for i:=0 to High(marked) do
      if marked[i] > curr then Dec(marked[i]);

    // remove the node itself
    Delete(NodeArray[SelectedPolygon], curr, 1);
  until Length(marked) = 0;

  if NodeArray[SelectedPolygon] = [] then
    Self.RemoveFromArray;

  SelectedNode := High(NodeArray[SelectedPolygon]);
  ImageBox.Update;

  if SelectedPolygon > -1 then
  begin
    PolyIndexEdit.setCaption(ToStr(SelectedPolygon));
    if NodeArray[SelectedPolygon] <> [] then
      CoordinateEdit.setCaption(ToStr(NodeArray[SelectedPolygon][SelectedNode].X) + ', ' + ToStr(NodeArray[SelectedPolygon][SelectedNode].Y))
    else
      CoordinateEdit.setCaption('null, null');
  end
  else
    PolyIndexEdit.setCaption('null');
end;


procedure TPolyDrawerForm.OnMouseDown(Sender: TObject; Button: TMouseButton; Shift: TShiftState; X, Y: Int32);
var
  I, J: Int32;
  P: TPoint;
begin
  if Button <> mbLeft then
    Exit;

  Dragging := True;

  for I := 0 to High(NodeArray) do
    for J := 0 to High(NodeArray[I]) do
    begin
      P := NodeArray[I][J] - Offset;
      if Distance(P.X, P.Y, X, Y) <= 3 then
      begin
        SelectedPolygon := I;
        SelectedNode := J;
        PolyIndexEdit.setCaption(ToStr(SelectedPolygon));
        CoordinateEdit.setCaption(ToStr(NodeArray[I][J].X) + ', ' + ToStr(NodeArray[I][J].Y));
        ImageBox.Update;
        Exit;
      end;
    end;

  Self.AddNode([X, Y]);
end;

procedure TPolyDrawerForm.OnMouseUp(Sender: TObject; Button: TMouseButton; Shift: TShiftState; X, Y: Int32);
begin
  if Button = mbLeft then
    Dragging := False;
end;

procedure TPolyDrawerForm.OnKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
  if (Key = VK_DELETE) and (SelectedNode > -1) then
    DeleteNode(SelectedNode);
end;


procedure TPolyDrawerForm.DrawPolygons(Area: TBox);
var
  I, J, Color: Int32;
  P, Q: TPoint;
  Canvas: TCanvas;
begin
  if (Length(NodeArray) = 0) then
    Exit;

  Canvas := ImageBox.GetOverlay.GetCanvas;
  for J := 0 to High(NodeArray) do
  begin
    if (Length(NodeArray[J]) > 0) then
    begin
      for i := 0 to High(NodeArray[J]) do
      begin
        P := NodeArray[J][i] - Offset;

        if I < High(NodeArray[J]) then
          Q := NodeArray[J][I + 1]
        else
          Q := NodeArray[J][0];

        Q := Q - Offset;

        if J = SelectedPolygon then
        begin
          Canvas.GetPen.SetColor($00FF7F);
          Color := $FFAAD4;
        end
        else
        begin
          Canvas.GetPen.SetColor($00FFFF);
          Color := $0000FF;
        end;

        if P.InBox(Area) or Q.InBox(Area) then
        begin
          Canvas.MoveTo(P.x, P.y);
          Canvas.LineTo(Q.x, Q.y);
        end;

        if P.InBox(Area) then
        begin
          Canvas.GetBrush.SetColor(Color);
          Canvas.FillRect(P.X-3, P.Y-3, P.X+3, P.Y+3);
        end;
      end;

      if InRange(SelectedNode, Low(NodeArray[SelectedPolygon]), High(NodeArray[SelectedPolygon])) then
      begin
        Canvas.GetBrush.SetColor($FF0000);
        P := NodeArray[SelectedPolygon][SelectedNode] - Offset;
        Canvas.FillRect(P.X-3, P.Y-3, P.X+3, P.Y+3);
      end;
    end;
  end;
end;


procedure TPolyDrawerForm.PaintArea(Sender: TObject; Canvas: TCanvas; R: TRect);
begin
  DrawPolygons([R.Left, R.Top, R.Right, R.Bottom]);
end;


procedure TPolyDrawerForm.OnUnSelectNode(Sender: TObject);
begin
  SelectedNode := -1;
  ImageBox.Update;
end;


procedure TPolyDrawerForm.OnResetNodeArray(Sender: TObject);
begin
  NodeArray := [];
  SelectedPolygon := -1;
  SelectedNode := -1;
end;

procedure TPolyDrawerForm.OnNewNodeArray(Sender: TObject);
begin
  Self.AddToArray;
end;

procedure TPolyDrawerForm.OnPrintNodeArray(Sender: TObject);
var
  I, J: Int32;
  Str: String;
begin
  if Length(NodeArray) > 1 then
  begin
    Str := '[' + STR_NEW_LINE;
    for J := 0 to High(NodeArray) do
    begin
      Str += '[';
      for I := 0 to High(NodeArray[J]) do
      begin
        Str += '[' + ToStr(NodeArray[J][I].X) + ', ' + ToStr(NodeArray[J][I].Y) + ']';
        if I < High(NodeArray[J]) then
          Str += ', ';
      end;

      Str += ']';
      if J < High(NodeArray) then
        Str += ',';

      Str += STR_NEW_LINE;
    end;

    Str += ']';
  end
  else
  begin
      Str := '[';
      for I := 0 to High(NodeArray[0]) do
      begin
        Str += '[' + ToStr(NodeArray[0][I].X) + ', ' + ToStr(NodeArray[0][I].Y) + ']';
        if I < High(NodeArray[0]) then
          Str += ', ';
      end;
      Str += ']';
  end;

  WriteLn Str;
end;


procedure TPolyDrawerForm.OnFormShow(Sender: TObject);
begin
  ImageBox.Update;
  PolyIndexEdit.setCaption('null');
  CoordinateEdit.setCaption('null, null');
end;

procedure TPolyDrawerForm.OnFormClose(Sender: TObject; var CloseAction: TCloseAction);
begin
  OnPrintNodeArray(nil);
end;

procedure TPolyDrawerForm.Run;
var
  Panel: TPanel;
  IndexLabel, NodeLabel: TLabel;
  PrintButton, NewButton, ResetButton: TButton;
begin
  try
    Name := 'PolygonDrawer';

    Form.Init(nil);
    Form.SetPosition(poScreenCenter);
    Form.SetCaption('Walker Polygon Drawer');
    Form.SetWidth(1000);
    Form.SetHeight(1000);
    Form.GetFont.SetSize(10);
    Form.SetOnShow(@OnFormShow);
    Form.SetOnClose(@OnFormClose);

    Panel.Init(Form);
    Panel.SetParent(Form);
    Panel.SetAutoSize(True);
    Panel.SetAlign(alTop);

    CoordinateEdit.Init(Panel);
    CoordinateEdit.setParent(Panel);
    CoordinateEdit.setAutoSize(True);
    CoordinateEdit.setAlign(alLeft);

    NodeLabel.Init(Panel);
    NodeLabel.setParent(Panel);
    NodeLabel.setAutoSize(True);
    NodeLabel.setCaption(' Selected coordinate: ');
    NodeLabel.setAlign(alLeft);

    PolyIndexEdit.Init(Panel);
    PolyIndexEdit.setParent(Panel);
    PolyIndexEdit.setAutoSize(True);
    PolyIndexEdit.setAlign(alLeft);

    IndexLabel.Init(Panel);
    IndexLabel.setParent(Panel);
    IndexLabel.setAutoSize(True);
    IndexLabel.setCaption(' Selected polygon: ');
    IndexLabel.setAlign(alLeft);

    ResetButton.Init(Panel);
    ResetButton.SetParent(Panel);
    ResetButton.SetAutoSize(True);
    ResetButton.SetCaption('Reset');
    ResetButton.SetOnClick(@OnResetNodeArray);
    ResetButton.setAlign(alLeft);

    NewButton.Init(Panel);
    NewButton.SetParent(Panel);
    NewButton.SetAutoSize(True);
    NewButton.SetCaption('New Polygon');
    NewButton.SetOnClick(@OnNewNodeArray);
    NewButton.setAlign(alLeft);

    PrintButton.Init(Panel);
    PrintButton.SetParent(Panel);
    PrintButton.SetAutoSize(True);
    PrintButton.SetCaption('Print Polygons');
    PrintButton.SetOnClick(@OnPrintNodeArray);
    PrintButton.setAlign(alLeft);

    ImageBox.Init(Form);
    ImageBox.SetParent(Form);
    ImageBox.SetAlign(alClient);
    ImageBox.SetOnPaintArea(@PaintArea);
    ImageBox.SetOnMouseMove(@OnMouseMove);
    ImageBox.SetOnMouseDown(@OnMouseDown);
    ImageBox.SetOnMouseUp(@OnMouseUp);
    ImageBox.SetOnKeyDown(@OnKeyDown);
    ImageBox.GetBackground.LoadFromFile(Map);
    ImageBox.BackgroundChanged;

    WriteLn('Click:                Add or select node'    + LineEnding +
            'Click + Drag:         Move selected node'    + LineEnding +
            'Press Delete Key:     Remove Selected Node'  + LineEnding +
            'Right Click + Drag:   Move around'           + LineEnding +
            'Ctrl + Mouse Wheel:   Zoom');

    Form.ShowModal;
    Form.Free;
  except
    Writeln GetExceptionMessage;
  end;
end;

begin
  Sync(@PolyDrawer.Run);
end.

